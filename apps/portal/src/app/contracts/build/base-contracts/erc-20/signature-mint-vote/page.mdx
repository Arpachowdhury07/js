import { Details } from "@doc";
import { GithubButtonLink } from "@doc";
import { createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "Contrato ERC20SignatureMintVote",
		icon: "contract",
	},
	title: "ERC20SignatureMintVote | contratos de thirdweb",
	description:
		"El contrato inteligente ERC20SignatureMintVote agrega la extensión de minting basado en firma al contrato base ERC20Vote",
});

# ERC20SignatureMintVote

El contrato inteligente `ERC20SignatureMintVote` agrega la extensión de [minting basado en firma](/contracts/build/extensions/erc-20/ERC20signaturemint)
al contrato base [ERC20Vote](/contracts/build/base-contracts/erc-20/base).
También implementa el contrato [ERC20Votes](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/extensions/ERC20Votes.sol), que proporciona funcionalidades de votación y delegación, junto con delegación por firma.
El [minting por firma](/glossary/signature-based-minting) usa [EIP-712](https://eips.ethereum.org/EIPS/eip-712),
lo que permite al administrador del contrato autorizar la solicitud de una parte externa para acuñar tokens en el contrato del administrador.
A un alto nivel, esto significa que puedes autorizar a otros a acuñar tokens
en tu contrato, y especificar qué exactamente será acuñado por esa parte externa.

```solidity
import "@thirdweb-dev/contracts/base/ERC20SignatureMintVote.sol";
```

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/base/ERC20SignatureMint.sol" />

## Uso

Importa el contrato y herédalo de él.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/base/ERC20SignatureMintVote.sol";

contract MyToken is ERC20SignatureMintVote {
	  constructor(
        address _defaultAdmin,
        string memory _name,
        string memory _symbol,
        address _primarySaleRecipient
    )
        ERC20SignatureMintVote(
            _defaultAdmin,
            _name,
            _symbol,
            _primarySaleRecipient
        )
    {}
}
```

## Extensiones detectadas

Una vez desplegado, puedes usar las funciones disponibles de estas extensiones en el SDK y el panel:

- [ERC20](/contracts/build/extensions/erc-20/ERC20)
- [ERC20Mintable](/contracts/build/extensions/erc-20/ERC20BatchMintable)
- [ERC20BatchMintable](/contracts/build/extensions/erc-20/ERC20BatchMintable)
- [ERC20SignatureMintable](/contracts/build/extensions/erc-20/ERC20signaturemint)
- [ERC20Permit](/contracts/build/extensions/erc-20/ERC20Permit)
- [PrimarySale](/contracts/build/extensions/general/PrimarySale)
- [ContractMetadata](/contracts/build/extensions/general/ContractMetadata)
- [Ownable](/contracts/build/extensions/general/Ownable)

## Funciones para sobrescribir

Las siguientes funciones han sido implementadas en este contrato y están disponibles para ser sobrescritas con lógica personalizada:

<Details id="mintWithSignature" summary="mintWithSignature">

```solidity
/**
 *  @notice           Minta tokens de acuerdo con la solicitud de mint proporcionada.
 *
 *  @param _req       La carga útil / solicitud de mint.
 *  @param _signature La firma producida por una cuenta que firma la solicitud de mint.
 */
function mintWithSignature(MintRequest calldata _req, bytes calldata _signature)
    external
    payable
    virtual
    returns (address signer)
{
    require(_req.quantity > 0, "Minting zero tokens.");

    // Verify and process payload.
    signer = _processRequest(_req, _signature);

    /**
        *  Get receiver of tokens.
        *
        *  Note: If `_req.to == address(0)`, a `mintWithSignature` transaction sitting in the
        *        mempool can be frontrun by copying the input data, since the minted tokens
        *        will be sent to the `_msgSender()` in this case.
        */
    address receiver = _req.to == address(0) ? msg.sender : _req.to;

    // Collect price
    _collectPriceOnClaim(_req.primarySaleRecipient, _req.quantity, _req.currency, _req.pricePerToken);

    // Mint tokens.
    _mint(receiver, _req.quantity);

    emit TokensMintedWithSignature(signer, receiver, _req);
}
```

</Details>

<Details id="_collectPriceOnClaim" summary="_collectPriceOnClaim">

```solidity
/// @dev Recoge y distribuye el valor de la venta principal de los tokens que se están reclamando.
function _collectPriceOnClaim(
    address _primarySaleRecipient,
    uint256 _quantityToClaim,
    address _currency,
    uint256 _pricePerToken
) internal virtual {
    if (_pricePerToken == 0) {
        return;
    }

    uint256 totalPrice = (_quantityToClaim * _pricePerToken) / 1 ether;
    require(totalPrice > 0, "quantity too low");

    if (_currency == CurrencyTransferLib.NATIVE_TOKEN) {
        require(msg.value == totalPrice, "Must send total price.");
    }

    address saleRecipient = _primarySaleRecipient == address(0) ? primarySaleRecipient() : _primarySaleRecipient;
    CurrencyTransferLib.transferCurrency(_currency, msg.sender, saleRecipient, totalPrice);
}
```

</Details>
