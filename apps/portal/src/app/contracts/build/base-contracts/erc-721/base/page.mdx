import { Details } from "@doc";
import { GithubButtonLink } from "@doc";
import { createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "Contrato ERC721Base",
		icon: "contract",
	},
	title: "ERC721Base | contratos thirdweb",
	description:
		"El contrato inteligente ERC721Base implementa el estándar ERC721 NFT, junto con la optimización ERC721A al estándar",
});

# ERC721Base

El contrato inteligente `ERC721Base` implementa el estándar [ERC721](https://eips.ethereum.org/EIPS/eip-721) NFT,
junto con la optimización [ERC721A](https://www.erc721a.org/) al estándar.
Permite acuñar NFTs para ti (o para otros) y vender esos NFTs en un mercado.

```solidity
import "@thirdweb-dev/contracts/base/ERC721Base.sol";
```

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/base/ERC721Base.sol" />

## Uso

Importa el contrato y hereda de él.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/base/ERC721Base.sol";

contract MyNFT is ERC721Base {

	  constructor(
        address _defaultAdmin,
        string memory _name,
        string memory _symbol,
        address _royaltyRecipient,
        uint128 _royaltyBps
    )
        ERC721Base(
            _defaultAdmin,
            _name,
            _symbol,
            _royaltyRecipient,
            _royaltyBps
        )
    {}

}
```

## Extensiones detectadas

Una vez desplegado, puedes usar las características habilitadas por estas extensiones en el SDK y el panel de control:

Haz clic en cada característica para aprender más sobre qué funciones están disponibles.

- [ERC721](/contracts/build/extensions/erc-721/ERC721)
- [ERC721Mintable](/contracts/build/extensions/erc-721/ERC721Mintable)
- [ERC721Burnable](/contracts/build/extensions/erc-721/ERC721Burnable)
- [ERC721BatchMintable](/contracts/build/extensions/erc-721/ERC721BatchMintable)
- [ERC721Supply](/contracts/build/extensions/erc-721/ERC721Supply)
- [Royalty](/contracts/build/extensions/general/Royalty)
- [ContractMetadata](/contracts/build/extensions/general/ContractMetadata)
- [Ownable](/contracts/build/extensions/general/Ownable)

## Funciones a sobrescribir

Las siguientes funciones han sido implementadas en este contrato y están disponibles para ser sobrescritas para agregar lógica personalizada:

<Details id="tokenURI" summary="tokenURI">

```solidity
    /**
     *  @notice         Devuelve la URI de metadatos para un NFT.
     *  @dev            Consulta `BatchMintMetadata` para el manejo de metadatos en este contrato.
     *
     *  @param _tokenId El tokenId de un NFT.
     */
    function tokenURI(uint256 _tokenId) public view virtual override returns (string memory) {
        string memory fullUriForToken = fullURI[_tokenId];
        if (bytes(fullUriForToken).length > 0) {
            return fullUriForToken;
        }

        string memory batchUri = _getBaseURI(_tokenId);
        return string(abi.encodePacked(batchUri, _tokenId.toString()));
    }

```

</Details>

<Details id="mintTo" summary="mintTo">

```solidity
   /**
     *  @notice          Permite que una dirección autorizada acuñe un NFT para un destinatario.
     *  @dev             La lógica en la función `_canMint` determina si la llamada está autorizada para acuñar NFTs.
     *
     *  @param _to       El destinatario del NFT a acuñar.
     *  @param _tokenURI La URI completa de metadatos para el NFT acuñado.
     */
    function mintTo(address _to, string memory _tokenURI) public virtual {
        require(_canMint(), "Not authorized to mint.");
        _setTokenURI(nextTokenIdToMint(), _tokenURI);
        _safeMint(_to, 1, "");
    }
```

</Details>

<Details id="batchMintTo" summary="batchMintTo">

```solidity
   /**
     *  @notice          Permite que una dirección autorizada acuñe múltiples NFTs de una vez para un destinatario.
     *  @dev             La lógica en la función `_canMint` determina si la llamada está autorizada para acuñar NFTs.
     *
     *  @param _to       El destinatario del NFT a acuñar.
     *  @param _quantity La cantidad de NFTs a acuñar.
     *  @param _baseURI  La baseURI para los `n` NFTs acuñados. Los metadatos para cada NFT son `baseURI/tokenId`
     *  @param _data     Datos adicionales para pasar durante la acuñación del NFT.
     */
    function batchMintTo(
        address _to,
        uint256 _quantity,
        string memory _baseURI,
        bytes memory _data
    ) public virtual {
        require(_canMint(), "Not authorized to mint.");
        _batchMintMetadata(nextTokenIdToMint(), _quantity, _baseURI);
        _safeMint(_to, _quantity, _data);
    }
```

</Details>

<Details id="burn" summary="burn">

```solidity
   /**
     *  @notice         Permite que un propietario o un operador autorizado queme el NFT del tokenId dado.
     *  @dev            El `_burn(uint256,bool)` de ERC721A verifica internamente las aprobaciones del token.
     *
     *  @param _tokenId El tokenId del NFT a quemar.
     */
    function burn(uint256 _tokenId) external virtual {
        _burn(_tokenId, true);
    }

    /*//////////////////////////////////////////////////////////////
                        Public getters
    //////////////////////////////////////////////////////////////*/

    /// @notice El tokenId asignado al siguiente nuevo NFT que se va a acuñar.
    function nextTokenIdToMint() public view virtual returns (uint256) {
        return _currentIndex;
    }

```

</Details>

<Details id="transferFrom" summary="transferFrom">

```solidity
    /// @dev See {ERC721-_transferFrom}.
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override(ERC721A) onlyAllowedOperator(from) {
        super.transferFrom(from, to, tokenId);
    }
```

</Details>

<Details id="safeTransferFrom" summary="safeTransferFrom">
```solidity
    /// @dev See {ERC721-_safeTransferFrom}.
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override(ERC721A) onlyAllowedOperator(from) {
        super.safeTransferFrom(from, to, tokenId);
    }
```
</Details>
