import { Details } from "@doc";
import { GithubButtonLink } from "@doc";
import { createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "ERC721SignatureMint contract",
		icon: "contract",
	},
	title: "ERC721SignatureMint | thirdweb contracts",
	description:
		"El contrato inteligente ERC721SignatureMint utiliza el contrato base ERC721Base junto con la extensión de contrato SignatureMintERC721",
});

# ERC721SignatureMint

```solidity
import "@thirdweb-dev/contracts/base/ERC721SignatureMint.sol";
```

# ERC721SignatureMint

El contrato inteligente `ERC721SignatureMint` utiliza el
[contrato base ERC721Base](/contracts/build/base-contracts/erc-721/base)
junto con la extensión de contrato [SignatureMintERC721](/contracts/build/extensions/erc-721/ERC721SignatureMint).

[Signature minting](/glossary/signature-based-minting) utiliza el estándar [EIP-712](https://eips.ethereum.org/EIPS/eip-712),
lo que permite que un administrador de contrato autorice la solicitud de una parte externa para acuñar tokens en el contrato del administrador.
A un alto nivel, esto significa que puedes autorizar a otra billetera a acuñar tokens
en tu contrato, y especificar qué exactamente será acuñado por esa parte externa.

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/base/ERC721SignatureMint.sol" />

## Uso

Importa la extensión del contrato y haz que tu contrato la herede.

```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/base/ERC721SignatureMint.sol";

contract MyNFT is ERC721SignatureMint {
    constructor(
        address _defaultAdmin,
        string memory _name,
        string memory _symbol,
        address _royaltyRecipient,
        uint128 _royaltyBps,
        address _primarySaleRecipient
    )
        ERC721SignatureMint(
            _defaultAdmin,
            _name,
            _symbol,
            _royaltyRecipient,
            _royaltyBps,
            _primarySaleRecipient
        )
    {}
}
```

## Extensiones Detectadas

Una vez desplegado, puedes usar las funciones disponibles de estas extensiones en el SDK y el panel de control:

Haz clic en cada característica para aprender más sobre qué funciones están disponibles.

- [ERC721](/contracts/build/extensions/erc-721/ERC721)
- [ERC721Burnable](/contracts/build/extensions/erc-721/ERC721Burnable)
- [ERC721Mintable](/contracts/build/extensions/erc-721/ERC721Mintable)
- [ERC721BatchMintable](/contracts/build/extensions/erc-721/ERC721BatchMintable)
- [ERC721Supply](/contracts/build/extensions/erc-721/ERC721Supply)
- [ERC721SignatureMintV2](/contracts/build/extensions/erc-721/ERC721SignatureMint)
- [PrimarySale](/contracts/build/extensions/general/PrimarySale)
- [Royalty](/contracts/build/extensions/general/Royalty)
- [ContractMetadata](/contracts/build/extensions/general/ContractMetadata)
- [Ownable](/contracts/build/extensions/general/Ownable)

## Funciones para Sobrescribir

Las siguientes funciones han sido implementadas en este contrato y están disponibles para ser sobrescritas para agregar lógica personalizada:

<Details id="mintWithSignature" summary="mintWithSignature">

```solidity
/**
*  @notice           Minta tokens según la solicitud de mint proporcionada.
*
*  @param _req       La carga útil / solicitud de mint.
*  @param _signature La firma producida por una cuenta que firma la solicitud de mint.
*/
function mintWithSignature(MintRequest calldata _req, bytes calldata _signature)
    external
    payable
    virtual
    override
    returns (address signer)
{
    require(_req.quantity == 1, "quantity must be 1");

    uint256 tokenIdToMint = nextTokenIdToMint();

    // Verify and process payload.
    signer = _processRequest(_req, _signature);

    address receiver = _req.to;

    // Collect price
    _collectPriceOnClaim(_req.primarySaleRecipient, _req.quantity, _req.currency, _req.pricePerToken);

    // Set royalties, if applicable.
    if (_req.royaltyRecipient != address(0) && _req.royaltyBps != 0) {
        _setupRoyaltyInfoForToken(tokenIdToMint, _req.royaltyRecipient, _req.royaltyBps);
    }

    // Mint tokens.
    _setTokenURI(tokenIdToMint, _req.uri);
    _safeMint(receiver, _req.quantity);

    emit TokensMintedWithSignature(signer, receiver, tokenIdToMint, _req);
}
```

</Details>

<Details id="_collectPriceOnClaim" summary="_collectPriceOnClaim">

```solidity
/// @dev Recoge y distribuye el valor de la venta primaria de los NFTs que están siendo reclamados.
function _collectPriceOnClaim(
    address _primarySaleRecipient,
    uint256 _quantityToClaim,
    address _currency,
    uint256 _pricePerToken
) internal virtual {
    if (_pricePerToken == 0) {
        return;
    }

    uint256 totalPrice = _quantityToClaim * _pricePerToken;

    if (_currency == CurrencyTransferLib.NATIVE_TOKEN) {
        require(msg.value == totalPrice, "Must send total price.");
    }

    address saleRecipient = _primarySaleRecipient == address(0) ? primarySaleRecipient() : _primarySaleRecipient;
    CurrencyTransferLib.transferCurrency(_currency, msg.sender, saleRecipient, totalPrice);
}
```

</Details>
