import { Details, Callout } from "@doc";
import { GithubButtonLink } from "@doc";
import { createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "Contrato de Cuenta",
		icon: "contract",
	},
	title: "Cuenta | contratos de thirdweb",
	description:
		"El contrato de Cuenta es un contrato inteligente no actualizable, simple, que viene con todos los beneficios básicos de la abstracción de cuentas",
});

# Cuenta

```solidity
import "@thirdweb-dev/contracts/smart-wallet/non-upgradeable/Account.sol";
```

Este contrato hereda del contrato [`BaseAccount`](/contracts/build/extensions/erc-4337/SmartWallet)

El contrato de Cuenta es una cuenta inteligente **no actualizable**, simple, que viene con todos los beneficios básicos de la abstracción de cuentas,
junto con las características predeterminadas:

- Tener múltiples propietarios
- Ejecutar transacciones (individuales y por lotes).
- Enviar y recibir tokens nativos.
- Enviar y recibir NFTs ERC-721 y ERC-1155.
- Capacidad para múltiples llamadas.
- Almacenar metadatos del contrato.

Los desarrolladores deben usar esta billetera si no anticipan hacer futuras actualizaciones a las billeteras de sus usuarios.

Los desarrolladores de aplicaciones pueden emitir cuentas inteligentes `Account` programáticamente mediante el despliegue de un contrato inteligente [`AccountFactory`](/contracts/build/base-contracts/erc-4337/account-factory).

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/prebuilts/account/non-upgradeable/Account.sol" />

<br />

<Callout title="Emitir cuentas programáticamente" variant="info" >

Si tienes la intención de emitir cuentas programáticamente utilizando un contrato de fábrica personalizado, debes hacer lo siguiente:

- Crear un nuevo contrato de fábrica extendiendo la extensión [BaseAccountFactory](/contracts/build/extensions/erc-4337/SmartWallet-factory).

- Sobrescribir la función `_initializeAccount` en la fábrica para crear una nueva billetera. ([referencia](https://github.com/thirdweb-dev/contracts/blob/main/contracts/smart-wallet/non-upgradeable/AccountFactory.sol#L36))

</Callout>

## Extensiones detectadas

Una vez desplegado, puedes usar las características disponibles por estas extensiones en el SDK y el panel:

Haz clic en cada característica para aprender más sobre qué funciones están disponibles.

- [SmartWallet](/contracts/build/extensions/erc-4337/SmartWallet)
- [Multicall](/contracts/build/extensions/general/Multicall)

## Uso

Importa el contrato y haz que herede de él. Este es un contrato de ejemplo que demuestra una forma en que podrías sobrescribir la funcionalidad para crear una cuenta vinculada a un token.

```solidity
import "@thirdweb-dev/contracts/smart-wallet/non-upgradable/Account.sol";
import "@thirdweb-dev/contracts/eip/interface/IERC721.sol";

contract TokenBoundAccount is Account {
    uint256 chainId;
    address tokenContract;
    uint256 tokenId;

    constructor(
        IEntryPoint _entrypoint,
        address _factory
    ) Account(_entrypoint, _factory) {
        _disableInitializers();
    }

    function isValidSigner(
        address _signer
    ) public view override returns (bool) {
        return (isOwner(_signer));
    }

    function isOwner(address _signer) public view returns (bool) {
        if (chainId != block.chainid) {
            revert("Invalid chainId");
        }
        return _signer == IERC721(tokenContract).ownerOf(tokenId);
    }

    function initialize(
        address _admin,
        bytes calldata _data
    ) public override initializer {
        super.initialize(_admin);
        (chainId, tokenContract, tokenId) = abi.decode(
            _data,
            (uint256, address, uint256)
        );
    }
}
```

## Funciones para sobrescribir

Las siguientes funciones han sido implementadas en este contrato y están disponibles para ser sobrescritas para agregar lógica personalizada:

<Details id="initialize" summary="initialize">

Inicializa la billetera de contrato inteligente.

```solidity
function initialize(address _defaultAdmin, bytes calldata _data) public virtual initializer {
    _setupRole(DEFAULT_ADMIN_ROLE, _defaultAdmin);
}
```

#### \_defaultAdmin

La cuenta admin. Debe ser de tipo `address`.

#### \_data

Los datos codificados en ABI para inicializar el contrato. Deben ser de tipo `bytes`.

</Details>

<Details id="getImplementationForFunction" summary="getImplementationForFunction">

Devuelve la dirección de implementación de la extensión, almacenada en el enrutador, para la función dada.

```solidity
function getImplementationForFunction(bytes4 _functionSelector) public view virtual override returns (address) {
    address impl = getExtensionForFunction(_functionSelector).implementation;
    return impl != address(0) ? impl : defaultExtension;
}
```

#### \_functionSelector

El selector de función para obtener la dirección de implementación de la extensión. Debe ser de tipo `bytes4`.

</Details>

<Details id="isValidSigner" summary="isValidSigner">

Devuelve si un firmante está autorizado para realizar transacciones usando la billetera.

```solidity
function isValidSigner(address _signer) public view virtual returns (bool) {
    return _hasRole(SIGNER_ROLE, _signer) || _hasRole(DEFAULT_ADMIN_ROLE, _signer);
}
```

#### \_signer

El firmante para verificar la autorización. Debe ser de tipo `address`.

</Details>

<Details id="entryPoint" summary="entryPoint">

Devuelve la dirección del contrato de punto de entrada.

```solidity
function entryPoint() public view virtual override returns (IEntryPoint) {
    return entrypointContract;
}
```

</Details>

<Details id="_canSetExtension" summary="_canSetExtension">

Devuelve si una extensión puede ser configurada en el contexto de ejecución dado.

```solidity
function _canSetExtension() internal view virtual override returns (bool) {
    return _hasRole(EXTENSION_ADMIN_ROLE, msg.sender);
}
```

</Details>

<Details id="_validatesSignature" summary="_validatesSignature">
Valida la firma de una operación de usuario.

```solidity
function _validateSignature(UserOperation calldata userOp, bytes32 userOpHash)
    internal
    virtual
    override
    returns (uint256 validationData)
{
    bytes32 hash = userOpHash.toEthSignedMessageHash();
    address signer = hash.recover(userOp.signature);

    if (!isValidSigner(signer)) return SIG_VALIDATION_FAILED;
    return 0;
}
```

</Details>
