import { Details, GithubButtonLink } from "@doc";
import { createMetadata } from "@doc";

export const metadata = createMetadata({
  image: {
    title: "Contrato ERC721SignatureMint",
    icon: "solidity",
  },
  title: "ERC721SignatureMint | contrato de thirdweb",
  description:
    "El contrato SignatureMintERC721 es una extensión destinada a distribuir tokens ERC721",
});


# ERC721SignatureMint

```solidity
import "@thirdweb-dev/contracts/extension/SignatureMintERC721.sol";
```

The `SignatureMintERC721` smart contract is an extension meant for distributing ERC721 tokens.

El mecanismo de ['firma de minting'](/glossary/signature-based-minting) en la extensión `SignatureMintERC721` usa [EIP-712](https://eips.ethereum.org/EIPS/eip-712), y es una manera en la que un administrador de contrato puede autorizar la solicitud de una parte externa para acuñar tokens en el contrato del administrador. A un alto nivel, esto significa que puedes autorizar a una parte externa para acuñar tokens en tu contrato y especificar qué exactamente será acuñado por esa parte externa.

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/extension/SignatureMintERC721.sol" />

## Uso

La extensión `SignatureMintERC721` es un _contrato abstracto_, y espera que implementes las siguientes funciones por ti mismo:

| Nombre                                                                            | Tipo                    | Descripción                                                                                                                                     |
| -------------------------------------------------------------------------------- | ----------------------- | ----------------------------------------------------------------------------------------------------------------------------------------------- |
| [`_canSignMintRequest`](/contracts/build/extensions/erc-721/ERC721SignatureMint) | interno de vista _virtual_ | Se ejecuta en cada intento de acuñar tokens con firma en el contrato. Devuelve si el firmante está autorizado para emitir una firma para acuñar. |
| [`mintWithSignature`](/contracts/build/extensions/erc-721/ERC721SignatureMint)   | externo pagadero        | Acuña tokens según la solicitud de acuñación proporcionada.                                                                                        |

Este es un contrato inteligente de ejemplo que demuestra cómo heredar de esta extensión y sobrescribir las funciones para agregar (opcionalmente) funcionalidad personalizada.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/base/ERC721Base.sol";
import "@thirdweb-dev/contracts/extension/SignatureMintERC721.sol";
import "@thirdweb-dev/contracts/lib/CurrencyTransferLib.sol";

contract ERC721SignatureMint is ERC721Base, SignatureMintERC721 {
    constructor(
        address _defaultAdmin,
        string memory _name,
        string memory _symbol,
        address _royaltyRecipient,
        uint128 _royaltyBps
    ) ERC721Base(_defaultAdmin, _name, _symbol, _royaltyRecipient, _royaltyBps) {}

    /// @dev Mints tokens according to the provided mint request.
    function mintWithSignature(
        MintRequest calldata _req,
        bytes calldata _signature
    ) external payable virtual override returns (address signer) {
        require(_req.quantity == 1, "quantity must be 1");

        uint256 tokenIdToMint = nextTokenIdToMint();

        // Verify and process payload.
        signer = _processRequest(_req, _signature);

        address receiver = _req.to;

        // Collect price
        _collectPriceOnClaim(
            _req.primarySaleRecipient,
            _req.quantity,
            _req.currency,
            _req.pricePerToken
        );

        // Mint tokens.
        _setTokenURI(tokenIdToMint, _req.uri);
        _safeMint(receiver, _req.quantity);

        emit TokensMintedWithSignature(signer, receiver, tokenIdToMint, _req);
    }

    /// @dev Returns whether a given address is authorized to sign mint requests.
    function _canSignMintRequest(address _signer)
        internal
        view
        virtual
        override
        returns (bool)
    {
        return _signer == owner();
    }

    /// @dev Collects and distributes the primary sale value of tokens being claimed.
    function _collectPriceOnClaim(
        address _primarySaleRecipient,
        uint256 _quantityToClaim,
        address _currency,
        uint256 _pricePerToken
    ) internal virtual {
        if (_pricePerToken == 0) {
            return;
        }

        uint256 totalPrice = (_quantityToClaim * _pricePerToken) / 1 ether;
        require(totalPrice > 0, "quantity too low");

        if (_currency == CurrencyTransferLib.NATIVE_TOKEN) {
            require(msg.value == totalPrice, "Must send total price.");
        }

        address saleRecipient = _primarySaleRecipient;
        CurrencyTransferLib.transferCurrency(
            _currency,
            msg.sender,
            saleRecipient,
            totalPrice
        );
    }
}
```

## Contratos de Base que implementan esta extensión

- [ERC721SignatureMint](/contracts/build/base-contracts/erc-721/signature-mint)

## Referencia completa de la API

<Details summary="verify">

```solidity
struct MintRequest {
    address to;
    address royaltyRecipient;
    uint256 royaltyBps;
    address primarySaleRecipient;
    string uri;
    uint256 quantity;
    uint256 pricePerToken;
    address currency;
    uint128 validityStartTimestamp;
    uint128 validityEndTimestamp;
    bytes32 uid;
}

function verify(MintRequest calldata req, bytes calldata signature)
    public
    view
    returns (bool success, address signer)
```

- Verifica que una solicitud de mint sea válida y esté firmada por una billetera autorizada.
- Parámetro `req`: La solicitud de mint.
  - `to`: El destinatario de los tokens a mintear.
  - `royaltyRecipient`: El destinatario de las regalías de ventas secundarias del token minteado.
  - `royaltyBps`: El porcentaje de las ventas secundarias del token minteado que se toma como regalías.
  - `primarySaleRecipient`: El destinatario de los ingresos por ventas primarias del token minteado.
  - `uri`: La URI de los metadatos del token a mintear.
  - `quantity`: La cantidad de tokens a mintear.
  - `pricePerToken`: El precio a pagar por cada cantidad de tokens minteados.
  - `currency`: La moneda en la que se paga el precio por cada token minteado.
  - `validityStartTimestamp`: El timestamp unix después del cual la carga útil es válida.
  - `validityEndTimestamp`: El timestamp unix en el que la carga útil expira.
  - `uid`: Un identificador único para la carga útil.
- Parámetro `signature`: La firma producida por una billetera autorizada firmando la solicitud de mint.

</Details>

<Details summary="mintWithSignature">

```solidity
struct MintRequest {
    address to;
    address royaltyRecipient;
    uint256 royaltyBps;
    address primarySaleRecipient;
    string uri;
    uint256 quantity;
    uint256 pricePerToken;
    address currency;
    uint128 validityStartTimestamp;
    uint128 validityEndTimestamp;
    bytes32 uid;
}

function mintWithSignature(MintRequest calldata req, bytes calldata signature)
    external
    payable
    returns (address signer)
```

- Mintea tokens de acuerdo con la solicitud de mint proporcionada.
- Parámetro `req`: La solicitud de mint.
  - `to`: El destinatario de los tokens a mintear.
  - `royaltyRecipient`: El destinatario de las regalías de ventas secundarias del token minteado.
  - `royaltyBps`: El porcentaje de las ventas secundarias del token minteado que se tomará como regalías.
  - `primarySaleRecipient`: El destinatario de los ingresos de las ventas primarias del token minteado.
  - `uri`: La URI de los metadatos del token a mintear.
  - `quantity`: La cantidad de tokens a mintear.
  - `pricePerToken`: El precio a pagar por cada cantidad de tokens minteados.
  - `currency`: La moneda en la que se pagará el precio por cada token minteado.
  - `validityStartTimestamp`: El timestamp unix después del cual la carga útil es válida.
  - `validityEndTimestamp`: El timestamp unix en el que la carga útil expira.
  - `uid`: Un identificador único para la carga útil.
- Parámetro `signature`: La firma producida por una billetera autorizada firmando la solicitud de mint.

</Details>

<Details summary="_processRequest">

```solidity
struct MintRequest {
    address to;
    address royaltyRecipient;
    uint256 royaltyBps;
    address primarySaleRecipient;
    string uri;
    uint256 quantity;
    uint256 pricePerToken;
    address currency;
    uint128 validityStartTimestamp;
    uint128 validityEndTimestamp;
    bytes32 uid;
}

function _processRequest(MintRequest calldata req, bytes calldata signature)
    internal
    returns (address signer)
```

- Verifica una solicitud de mint y marca la solicitud y la firma como utilizadas.
- Parámetro `req`: La solicitud de mint.
  - `to`: El destinatario de los tokens a mintear.
  - `royaltyRecipient`: El destinatario de las regalías de ventas secundarias del token minteado.
  - `royaltyBps`: El porcentaje de las ventas secundarias del token minteado que se tomará como regalías.
  - `primarySaleRecipient`: El destinatario de los ingresos de las ventas primarias del token minteado.
  - `uri`: La URI de los metadatos del token a mintear.
  - `quantity`: La cantidad de tokens a mintear.
  - `pricePerToken`: El precio a pagar por cada cantidad de tokens minteados.
  - `currency`: La moneda en la que se pagará el precio por cada token minteado.
  - `validityStartTimestamp`: El timestamp unix después del cual la carga útil es válida.
  - `validityEndTimestamp`: El timestamp unix en el que la carga útil expira.
  - `uid`: Un identificador único para la carga útil.
- Parámetro `signature`: La firma producida por una billetera autorizada firmando la solicitud de mint.

</Details>

<Details summary="_recoverAddress">

```solidity
struct MintRequest {
    address to;
    address royaltyRecipient;
    uint256 royaltyBps;
    address primarySaleRecipient;
    string uri;
    uint256 quantity;
    uint256 pricePerToken;
    address currency;
    uint128 validityStartTimestamp;
    uint128 validityEndTimestamp;
    bytes32 uid;
}

function _recoverAddress(MintRequest calldata req, bytes calldata signature)
    internal
    view
    returns (address signer)
```

- Devuelve la dirección del firmante de la solicitud de mint.
- Parámetro `req`: La solicitud de mint.
  - `to`: El destinatario de los tokens a mintear.
  - `royaltyRecipient`: El destinatario de las regalías de ventas secundarias del token minteado.
  - `royaltyBps`: El porcentaje de las ventas secundarias del token minteado que se tomará como regalías.
  - `primarySaleRecipient`: El destinatario de los ingresos de las ventas primarias del token minteado.
  - `uri`: La URI de los metadatos del token a mintear.
  - `quantity`: La cantidad de tokens a mintear.
  - `pricePerToken`: El precio a pagar por cada cantidad de tokens minteados.
  - `currency`: La moneda en la que se pagará el precio por cada token minteado.
  - `validityStartTimestamp`: El timestamp unix después del cual la carga útil es válida.
  - `validityEndTimestamp`: El timestamp unix en el que la carga útil expira.
  - `uid`: Un identificador único para la carga útil.
- Parámetro `signature`: La firma producida por una billetera autorizada firmando la solicitud de mint.

</Details>

<Details summary="_canSignMintRequest">

```solidity
function _canSignMintRequest(address signer) internal view virtual returns (bool);
```

- Devuelve si una dirección dada está autorizada para firmar solicitudes de mint.
- Parámetro `signer`: la dirección que el contrato verifica si es elegible para firmar solicitudes de mint o no.

</Details>
