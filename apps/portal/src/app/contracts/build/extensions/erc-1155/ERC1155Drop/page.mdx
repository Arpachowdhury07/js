import { Details, GithubButtonLink } from "@doc";
import { createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "Contrato ERC1155Drop",
		icon: "solidity",
	},
	title: "ERC1155Drop | contrato de thirdweb",
	description:
		"El contrato Drop1155 es una extensión destinada a distribuir tokens ERC1155",
});


# ERC1155Drop

```solidity
import "@thirdweb-dev/contracts/extension/Drop1155.sol";
```

El contrato `Drop1155` es una extensión destinada a distribuir tokens [ERC1155](/contracts/build/extensions/erc-1155/ERC1155). Permite configurar múltiples fases de reclamo, a diferencia de [`ERC1155DropSinglePhase`](/contracts/build/extensions/erc-1155/ERC1155DropSinglePhase). Esta serie de fases de reclamo está ordenada por su respectivo `startTimestamp`.

Esta extensión implementa el mecanismo de distribución ['Drop'](/glossary/drop): establece múltiples fases de reclamo con restricciones como un precio a cobrar, una lista permitida, etc., para la acuñación pública / permitida de tus tokens.

Se recomienda usar esta extensión junto con la extensión [`LazyMint`](/contracts/build/extensions/general/LazyMint).

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/extension/Drop1155.sol" />

## Uso

La extensión `Drop1155` es un _contrato abstracto_ y se espera que implementes las siguientes funciones por ti mismo:

| Nombre                    | Tipo                    | Descripción                                                                                                                                      |
| ------------------------- | ----------------------- | ------------------------------------------------------------------------------------------------------------------------------------------------ |
| `_afterClaim`              | interno _virtual_       | Se ejecuta después de cada llamada a la función `claim`.                                                                                         |
| `_beforeClaim`             | interno _virtual_       | Se ejecuta antes de cada llamada a la función `claim`.                                                                                          |
| `_collectPriceOnClaim`     | interno _virtual_       | Recoge y distribuye el valor de la venta primaria de los NFTs reclamados.                                                                         |
| `_transferTokensOnClaim`   | interno _virtual_       | Transfiere los NFTs que se están reclamando.                                                                                                     |
| `_canSetClaimConditions`   | interno view _virtual_  | Se ejecuta en cada intento de establecer las condiciones de reclamo en el contrato. Devuelve si se pueden establecer las condiciones de reclamo en el contexto de ejecución dado. |

Este es un ejemplo de contrato inteligente que demuestra cómo heredar de esta extensión y sobrecargar las funciones para agregar (opcionalmente) funcionalidad personalizada.

```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/extension/Drop1155.sol";
import "@thirdweb-dev/contracts/base/ERC1155Base.sol";
import "@thirdweb-dev/contracts/lib/CurrencyTransferLib.sol";

/// Este es un EJEMPLO de uso de `Drop1155` para distribuir tokens ERC1155.

contract MyContract is ERC1155Base, Drop1155 {
    /*//////////////////////////////////////////////////////////////
                            Constructor
    //////////////////////////////////////////////////////////////*/

    constructor(
        string memory _name,
        string memory _symbol,
        address _royaltyRecipient,
        uint128 _royaltyBps
    ) ERC1155Base(_name, _symbol, _royaltyRecipient, _royaltyBps) {}

    /*//////////////////////////////////////////////////////////////
                        Internal (overrideable) functions
    //////////////////////////////////////////////////////////////*/

    /// @dev Collects and distributes the primary sale value of NFTs being claimed.
    function _collectPriceOnClaim(
        uint256 _tokenId,
        address _primarySaleRecipient,
        uint256 _quantityToClaim,
        address _currency,
        uint256 _pricePerToken
    ) internal virtual override {
        if (_pricePerToken == 0) {
            return;
        }

        uint256 totalPrice = _quantityToClaim * _pricePerToken;

        if (_currency == CurrencyTransferLib.NATIVE_TOKEN) {
            if (msg.value != totalPrice) {
                revert("Must send total price.");
            }
        }

        address saleRecipient = _primarySaleRecipient;
        CurrencyTransferLib.transferCurrency(_currency, msg.sender, saleRecipient, totalPrice);
    }

    /// @dev Transfers the tokens being claimed.
    function _transferTokensOnClaim(address _to, uint256 _tokenId, uint256 _quantityBeingClaimed)
        internal
        virtual
        override
    {
        _mint(_to, _tokenId, _quantityBeingClaimed, "");
    }

    /// @dev Checks whether platform fee info can be set in the given execution context.
    function _canSetClaimConditions()
        internal
        view
        virtual
        override
        returns (bool)
    {
        return msg.sender == owner();
    }

    /// @dev Runs before every `claim` function call.
    function _beforeClaim(
        uint256 _tokenId,
        address _receiver,
        uint256 _quantity,
        address _currency,
        uint256 _pricePerToken,
        AllowlistProof calldata _allowlistProof,
        bytes memory _data
    ) internal virtual {}

    /// @dev Runs after every `claim` function call.
    function _afterClaim(
        uint256 _tokenId,
        address _receiver,
        uint256 _quantity,
        address _currency,
        uint256 _pricePerToken,
        AllowlistProof calldata _allowlistProof,
        bytes memory _data
    ) internal virtual {}
}
```

## Contratos de Base que implementan esta extensión

Ninguno de los contratos base implementa esta extensión.

## Referencia completa de la API


<Details summary="claim">

```solidity
function claim(
    address receiver,
    uint256 tokenId,
    uint256 quantity,
    address currency,
    uint256 pricePerToken,
    AllowlistProof calldata allowlistProof,
    bytes memory data
) external payable;
```

- Permite que una cuenta reclame una cantidad dada de tokens.
- Parámetro `receiver`: El receptor de los tokens a reclamar.
- Parámetro `tokenId`: El tokenId de los NFTs a reclamar.
- Parámetro `quantity`: La cantidad de tokens a reclamar.
- Parámetro `currency`: La moneda con la que pagar por el reclamo.
- Parámetro `pricePerToken`: El precio por token para el reclamo.
- Parámetro `allowlistProof`: La prueba de inclusión del reclamante en la lista de merkle raíz.
- Parámetro `data`: Datos arbitrarios en bytes que pueden ser utilizados en la implementación de esta interfaz.

</Details>

<Details summary="setClaimConditions">

```solidity
struct ClaimCondition {
    uint256 startTimestamp;
    uint256 maxClaimableSupply;
    uint256 supplyClaimed;
    uint256 quantityLimitPerWallet;
    bytes32 merkleRoot;
    uint256 pricePerToken;
    address currency;
    string metadata;
}

function setClaimConditions(uint256 tokenId, ClaimCondition[] calldata phases, bool resetClaimEligibility) external;
```

- Permite que una billetera autorizada establezca las condiciones de reclamo para el contrato.
- Parámetro `tokenId`: El ID del token para el cual establecer las condiciones de reclamo.
- Parámetro `phases`: El arreglo donde cada `fase` contiene un conjunto de restricciones que se aplicarán a la acuñación de tokens en el contrato.
  - `startTimestamp`: El timestamp unix después del cual la condición de reclamo aplica. La misma condición de reclamo se aplica hasta el `startTimestamp` de la siguiente condición de reclamo.
  - `maxClaimableSupply`: El número total máximo de tokens que pueden ser reclamados bajo la condición de reclamo.
  - `supplyClaimed`: En cualquier punto dado, el número de tokens que han sido reclamados bajo la condición de reclamo.
  - `quantityLimitPerWallet`: El número máximo de tokens que puede reclamar una billetera.
  - `merkleRoot`: La lista de direcciones que pueden reclamar tokens bajo la condición de reclamo.
  - `pricePerToken`: El precio requerido para pagar por cada token reclamado.
  - `currency`: La moneda en la que debe pagarse el precio.
  - `metadata`: Metadatos de la condición de reclamo.
- Parámetro `resetClaimEligibility`: Si se deben mantener las restricciones activas para las billeteras que reclamaron tokens en la condición de reclamo actual (por ejemplo, el próximo timestamp válido de reclamo para una billetera determinada).
- La función `_canSetClaimConditions` se ejecuta en cada llamada a esta función. Si el valor retornado de `_canSetClaimConditions` es `false`, la llamada a `setClaimConditions` revertirá.

</Details>

<Details summary="verifyClaim">

```solidity
struct AllowlistProof {
    bytes32[] proof;
    uint256 quantityLimitPerWallet;
    uint256 pricePerToken;
    address currency;
}

function verifyClaim(
    uint256 conditionId,
    address claimer,
    uint256 tokenId,
    uint256 quantity,
    address currency,
    uint256 pricePerToken,
    AllowlistProof calldata allowlistProof
) public view returns (bool isOverride);
```

- Verifica una solicitud para reclamar tokens contra los criterios de la condición de reclamo activa.
- Parámetro `conditionId`: ID de la condición de reclamo activa.
- Parámetro `claimer`: La billetera que está reclamando tokens.
- Parámetro `tokenId`: El tokenId de los NFTs a reclamar.
- Parámetro `quantity`: La cantidad de tokens a reclamar.
- Parámetro `currency`: La moneda en la que se debe pagar el precio por el reclamo.
- Parámetro `pricePerToken`: El precio a pagar por cada token reclamado.
- Parámetro `allowlistProof`: Estructura que contiene los siguientes elementos:
  - `proof`: Prueba de la inclusión de la billetera interesada en la lista de permitidos.
  - `quantityLimitPerWallet`: La cantidad total de tokens que la billetera en la lista de permitidos es elegible para reclamar con el tiempo.
  - `pricePerToken`: El precio por token que la billetera en la lista de permitidos debe pagar para reclamar tokens.
  - `currency`: La moneda en la que la billetera en la lista de permitidos debe pagar el precio para reclamar tokens.

</Details>

<Details summary="getActiveClaimConditionId">

```solidity
function getActiveClaimConditionId(uint256 tokenId) public view returns (uint256);
```

- Devuelve el ID de la condición de reclamo activa actualmente.
- Parámetro `tokenId`: El ID del token para el cual verificar la condición de reclamo activa.

</Details>

<Details summary="getClaimConditionById">

```solidity
function getClaimConditionById(uint256 tokenId, uint256 conditionId) external view returns (ClaimCondition memory);
```

- Devuelve la condición de reclamo para un ID de condición dado.
- Parámetro `tokenId`: El ID del token para el cual verificar la condición de reclamo.
- Parámetro `conditionId`: ID de la condición de reclamo que se está viendo.

</Details>

<Details summary="getSupplyClaimedByWallet">

```solidity
function getSupplyClaimedByWallet(uint256 tokenId, uint256 conditionId, address claimer) public view returns (uint256);
```

- Devuelve el número de tokens reclamados por una billetera bajo la condición dada.
- Parámetro `tokenId`: El ID del token para el cual verificar la cantidad reclamada.
- Parámetro `conditionId`: ID de la condición de reclamo bajo la cual verificar la cantidad reclamada.
- Parámetro `claimer`: La billetera que reclama los tokens.

</Details>

<Details summary="_dropMsgSender">

```solidity
function _dropMsgSender() internal virtual returns (address);
```

- Expone la capacidad de sobrescribir el remitente del mensaje.

</Details>

<Details summary="_beforeClaim">

```solidity
function _beforeClaim(
    uint256 tokenId,
    address receiver,
    uint256 quantity,
    address currency,
    uint256 pricePerToken,
    AllowlistProof calldata allowlistProof,
    bytes memory data
) internal virtual
```

- Se ejecuta antes de cada llamada a la función `claim`. Exposa la capacidad de agregar lógica personalizada que se ejecute antes de cada reclamo de tokens.
- Parámetro `tokenId`: El tokenId de los NFTs a reclamar.
- Parámetro `receiver`: El receptor de los tokens a reclamar.
- Parámetro `quantity`: La cantidad de tokens a reclamar.
- Parámetro `currency`: La moneda en la que se pagará el reclamo.
- Parámetro `pricePerToken`: El precio por token a pagar por el reclamo.
- Parámetro `allowlistProof`: La prueba de la inclusión del reclamante en la lista blanca de merkle.
- Parámetro `data`: Datos arbitrarios en bytes que pueden ser utilizados en la implementación de esta interfaz.

</Details>

<Details summary="_afterClaim">

```solidity
function _afterClaim(
    uint256 tokenId,
    address receiver,
    uint256 quantity,
    address currency,
    uint256 pricePerToken,
    AllowlistProof calldata allowlistProof,
    bytes memory data
) internal virtual
```

- Se ejecuta después de cada llamada a la función `claim`. Exposa la capacidad de agregar lógica personalizada que se ejecute después de cada reclamo de tokens.
- Parámetro `tokenId`: El tokenId de los NFTs a reclamar.
- Parámetro `receiver`: El receptor de los tokens a reclamar.
- Parámetro `quantity`: La cantidad de tokens a reclamar.
- Parámetro `currency`: La moneda en la que se pagará el reclamo.
- Parámetro `pricePerToken`: El precio por token a pagar por el reclamo.
- Parámetro `allowlistProof`: La prueba de la inclusión del reclamante en la lista blanca de merkle.
- Parámetro `data`: Datos arbitrarios en bytes que pueden ser utilizados en la implementación de esta interfaz.

</Details>

<Details summary="_collectPriceOnClaim">

```solidity
function _collectPriceOnClaim(
    uint256 tokenId,
    address primarySaleRecipient,
    uint256 quantityToClaim,
    address currency,
    uint256 pricePerToken
) internal virtual;
```

- Recoge y distribuye el valor de la venta primaria de los tokens siendo reclamados.
- Parámetro `tokenId`: El tokenId de los NFTs a reclamar.
- Parámetro `primarySaleRecipient`: El receptor del valor de la venta primaria generado por un reclamo.
- Parámetro `quantityToClaim`: La cantidad de tokens siendo reclamados.
- Parámetro `currency`: La moneda utilizada para pagar el reclamo.
- Parámetro `pricePerToken`: El precio por token a pagar por el reclamo.

</Details>

<Details summary="_transferTokensOnClaim">

```solidity
function _transferTokensOnClaim(address to, uint256 tokenId, uint256 quantityBeingClaimed)
    internal
    virtual;
```

- Transfiere los tokens siendo reclamados al receptor correspondiente.
- Parámetro `to`: El receptor de los tokens para un reclamo.
- Parámetro `tokenId`: El tokenId de los NFTs a reclamar.
- Parámetro `quantityBeingClaimed`: La cantidad de tokens siendo reclamados.

</Details>

<Details summary="_canSetClaimConditions">

```solidity
function _canSetClaimConditions() internal view virtual returns (bool);
```

- Se ejecuta en cada llamada a la función `setClaimConditions`.
- Devuelve si las condiciones de reclamación pueden ser establecidas en el contexto de ejecución dado.
- Por ejemplo, esta función puede verificar si la billetera que llama a `setClaimConditions` es el propietario del contrato, y forzar que solo el propietario pueda llamar exitosamente a `setClaimConditions`.

</Details>
