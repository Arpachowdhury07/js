import { Details, GithubButtonLink } from "@doc";
import { createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "Contrato ERC1155SignatureMint",
		icon: "solidity",
	},
	title: "ERC1155SignatureMint | contrato thirdweb",
	description:
		"El contrato SignatureMintERC1155 es una extensión destinada a distribuir tokens ERC1155",
});


# ERC1155SignatureMint

```solidity
import "@thirdweb-dev/contracts/extension/SignatureMintERC1155.sol";
```

El contrato `SignatureMintERC1155` es una extensión destinada a distribuir tokens ERC1155.

El mecanismo de ['firma de minting'](/glossary/signature-based-minting) en la extensión `SignatureMintERC1155` utiliza [EIP-712](https://eips.ethereum.org/EIPS/eip-712), y es una forma en que un administrador de contrato puede autorizar la solicitud de una parte externa para mintear tokens en el contrato del administrador. A un alto nivel, esto significa que puedes autorizar a una parte externa para mintear tokens en tu contrato, y especificar qué exactamente se minteará por esa parte externa.

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/extension/SignatureMintERC1155.sol" />

## Uso

La extensión `SignatureMintERC1155` es un contrato _abstracto_, y espera que implementes las siguientes funciones por ti mismo:

| Nombre                                                                                     | Tipo                    | Descripción                                                                                                                                       |
| -------------------------------------------------------------------------------------------- | ----------------------- | ------------------------------------------------------------------------------------------------------------------------------------------------- |
| [`_canSignMintRequest`](/contracts/build/extensions/erc-1155/ERC1155SignatureMint)           | interno view _virtual_  | Se ejecuta en cada intento de mintear tokens con firma en el contrato. Devuelve si el firmante está autorizado para emitir una firma para el minting. |
| [`mintWithSignature`](/contracts/build/extensions/erc-1155/ERC1155SignatureMint)             | externo payable         | Mintea tokens de acuerdo con la solicitud de minting proporcionada.                                                                               |

Este es un contrato inteligente de ejemplo que demuestra cómo heredar de esta extensión y sobrescribir las funciones para agregar (opcional) funcionalidades personalizadas.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/base/ERC1155Base.sol";
import "@thirdweb-dev/contracts/extension/SignatureMintERC1155.sol";
import "@thirdweb-dev/contracts/lib/CurrencyTransferLib.sol";

contract ERC1155SignatureMint is ERC1155Base, SignatureMintERC1155 {
    constructor(
        string memory _name,
        string memory _symbol,
        address _royaltyRecipient,
        uint128 _royaltyBps
    ) ERC1155Base(_name, _symbol, _royaltyRecipient, _royaltyBps) {}

    /// @dev Mints tokens according to the provided mint request.
    function mintWithSignature(
        MintRequest calldata _req,
        bytes calldata _signature
    ) external payable virtual override returns (address signer) {
        require(_req.quantity > 0, "Minting zero tokens.");

        uint256 tokenIdToMint;
        uint256 nextIdToMint = nextTokenIdToMint();

        if (_req.tokenId == type(uint256).max) {
            tokenIdToMint = nextIdToMint;
            nextTokenIdToMint_ += 1;
        } else {
            require(_req.tokenId < nextIdToMint, "invalid id");
            tokenIdToMint = _req.tokenId;
        }

        // Verify and process payload.
        signer = _processRequest(_req, _signature);

        address receiver = _req.to;

        // Collect price
        _collectPriceOnClaim(
            _req.primarySaleRecipient,
            _req.quantity,
            _req.currency,
            _req.pricePerToken
        );

        // Set royalties, if applicable.
        if (_req.royaltyRecipient != address(0)) {
            _setupRoyaltyInfoForToken(
                tokenIdToMint,
                _req.royaltyRecipient,
                _req.royaltyBps
            );
        }

        // Set URI
        if (_req.tokenId == type(uint256).max) {
            _setTokenURI(tokenIdToMint, _req.uri);
        }

        // Mint tokens.
        _mint(receiver, tokenIdToMint, _req.quantity, "");

        emit TokensMintedWithSignature(signer, receiver, tokenIdToMint, _req);
    }

    /// @dev Returns whether a given address is authorized to sign mint requests.
    function _canSignMintRequest(address _signer)
        internal
        view
        virtual
        override
        returns (bool)
    {
        return _signer == owner();
    }

    /// @dev Collects and distributes the primary sale value of tokens being claimed.
    function _collectPriceOnClaim(
        address _primarySaleRecipient,
        uint256 _quantityToClaim,
        address _currency,
        uint256 _pricePerToken
    ) internal virtual {
        if (_pricePerToken == 0) {
            return;
        }

        uint256 totalPrice = (_quantityToClaim * _pricePerToken) / 1 ether;
        require(totalPrice > 0, "quantity too low");

        if (_currency == CurrencyTransferLib.NATIVE_TOKEN) {
            require(msg.value == totalPrice, "Must send total price.");
        }

        address saleRecipient = _primarySaleRecipient;
        CurrencyTransferLib.transferCurrency(
            _currency,
            msg.sender,
            saleRecipient,
            totalPrice
        );
    }
}
```

## Contratos Base que Implementan Esta Extensión

- [ERC1155SignatureMint](/contracts/build/base-contracts/erc-1155/signature-mint)

## Referencia Completa de la API


<Details summary="verify">

```solidity
struct MintRequest {
    address to;
    address royaltyRecipient;
    uint256 royaltyBps;
    address primarySaleRecipient;
    uint256 tokenId;
    string uri;
    uint256 quantity;
    uint256 pricePerToken;
    address currency;
    uint128 validityStartTimestamp;
    uint128 validityEndTimestamp;
    bytes32 uid;
}

function verify(MintRequest calldata req, bytes calldata signature)
    public
    view
    returns (bool success, address signer)
```

- Verifica que una solicitud de minting sea válida y firmada por una cartera autorizada.
- Parámetro `req`: La solicitud de minting.
  - `to`: El destinatario de los tokens a mintear.
  - `royaltyRecipient`: El destinatario de las regalías de ventas secundarias del token minteado.
  - `royaltyBps`: El porcentaje de las ventas secundarias del token minteado que se tomará como regalías.
  - `primarySaleRecipient`: El destinatario de los ingresos de ventas primarias del token minteado.
  - `tokenId`: El tokenId del NFT a mintear.
  - `uri`: La URI de metadatos del token a mintear.
  - `quantity`: La cantidad de tokens a mintear.
  - `pricePerToken`: El precio a pagar por cada cantidad de tokens minteados.
  - `currency`: La moneda en la que se pagará el precio por token minteado.
  - `validityStartTimestamp`: El timestamp UNIX después del cual la carga útil es válida.
  - `validityEndTimestamp`: El timestamp UNIX en el cual la carga útil expira.
  - `uid`: Un identificador único para la carga útil.
- Parámetro `signature`: La firma producida por una cartera autorizada firmando la solicitud de minting.

</Details>

<Details summary="mintWithSignature">

```solidity
struct MintRequest {
    address to;
    address royaltyRecipient;
    uint256 royaltyBps;
    address primarySaleRecipient;
    uint256 tokenId;
    string uri;
    uint256 quantity;
    uint256 pricePerToken;
    address currency;
    uint128 validityStartTimestamp;
    uint128 validityEndTimestamp;
    bytes32 uid;
}

function mintWithSignature(MintRequest calldata req, bytes calldata signature)
    external
    payable
    returns (address signer)
```

- Mintea tokens de acuerdo con la solicitud de minting proporcionada.
- Parámetro `req`: La solicitud de minting.
  - `to`: El destinatario de los tokens a mintear.
  - `royaltyRecipient`: El destinatario de las regalías de ventas secundarias del token minteado.
  - `royaltyBps`: El porcentaje de las ventas secundarias del token minteado que se tomará como regalías.
  - `primarySaleRecipient`: El destinatario de los ingresos de ventas primarias del token minteado.
  - `tokenId`: El tokenId del NFT a mintear.
  - `uri`: La URI de metadatos del token a mintear.
  - `quantity`: La cantidad de tokens a mintear.
  - `pricePerToken`: El precio a pagar por cada cantidad de tokens minteados.
  - `currency`: La moneda en la que se pagará el precio por token minteado.
  - `validityStartTimestamp`: El timestamp UNIX después del cual la carga útil es válida.
  - `validityEndTimestamp`: El timestamp UNIX en el cual la carga útil expira.
  - `uid`: Un identificador único para la carga útil.
- Parámetro `signature`: La firma producida por una cartera autorizada firmando la solicitud de minting.

</Details>

<Details summary="_processRequest">

```solidity
struct MintRequest {
    address to;
    address royaltyRecipient;
    uint256 royaltyBps;
    address primarySaleRecipient;
    uint256 tokenId;
    string uri;
    uint256 quantity;
    uint256 pricePerToken;
    address currency;
    uint128 validityStartTimestamp;
    uint128 validityEndTimestamp;
    bytes32 uid;
}

function _processRequest(MintRequest calldata req, bytes calldata signature)
    internal
    returns (address signer)
```

- Verifica una solicitud de minting y marca la solicitud y la firma como utilizadas.
- Parámetro `req`: La solicitud de minting.
  - `to`: El destinatario de los tokens a mintear.
  - `royaltyRecipient`: El destinatario de las regalías de ventas secundarias del token minteado.
  - `royaltyBps`: El porcentaje de las ventas secundarias del token minteado que se tomará como regalías.
  - `primarySaleRecipient`: El destinatario de los ingresos de la venta primaria del token minteado.
  - `tokenId`: El tokenId del NFT a mintear.
  - `uri`: La URI de metadatos del token a mintear.
  - `quantity`: La cantidad de tokens a mintear.
  - `pricePerToken`: El precio a pagar por cada cantidad de tokens minteados.
  - `currency`: La moneda en la que se pagará el precio por token minteado.
  - `validityStartTimestamp`: El timestamp UNIX después del cual la carga útil es válida.
  - `validityEndTimestamp`: El timestamp UNIX en el cual la carga útil expira.
  - `uid`: Un identificador único para la carga útil.
- Parámetro `signature`: La firma producida por una cartera autorizada firmando la solicitud de minting.

</Details>

<Details summary="_recoverAddress">

```solidity
struct MintRequest {
    address to;
    address royaltyRecipient;
    uint256 royaltyBps;
    address primarySaleRecipient;
    uint256 tokenId;
    string uri;
    uint256 quantity;
    uint256 pricePerToken;
    address currency;
    uint128 validityStartTimestamp;
    uint128 validityEndTimestamp;
    bytes32 uid;
}

function _recoverAddress(MintRequest calldata req, bytes calldata signature)
    internal
    view
    returns (address signer)
```

- Devuelve la dirección del firmante de la solicitud de minting.
- Parámetro `req`: La solicitud de minting.
  - `to`: El destinatario de los tokens a mintear.
  - `royaltyRecipient`: El destinatario de las regalías de ventas secundarias del token minteado.
  - `royaltyBps`: El porcentaje de las ventas secundarias del token minteado que se tomará como regalías.
  - `primarySaleRecipient`: El destinatario de los ingresos de la venta primaria del token minteado.
  - `tokenId`: El tokenId del NFT a mintear.
  - `uri`: La URI de metadatos del token a mintear.
  - `quantity`: La cantidad de tokens a mintear.
  - `pricePerToken`: El precio a pagar por cada cantidad de tokens minteados.
  - `currency`: La moneda en la que se pagará el precio por token minteado.
  - `validityStartTimestamp`: El timestamp UNIX después del cual la carga útil es válida.
  - `validityEndTimestamp`: El timestamp UNIX en el cual la carga útil expira.
  - `uid`: Un identificador único para la carga útil.
- Parámetro `signature`: La firma producida por una cartera autorizada firmando la solicitud de minting.

</Details>

<Details summary="_canSignMintRequest">

```solidity
function _canSignMintRequest(address signer) internal view virtual returns (bool);
```

- Devuelve si una dirección dada está autorizada para firmar solicitudes de minting.
- Parámetro `signer`: la dirección que el contrato verifica si es elegible para firmar solicitudes de minting o no.

</Details>
