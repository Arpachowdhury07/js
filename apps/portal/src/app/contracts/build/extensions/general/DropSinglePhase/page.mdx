import { Details, Callout, GithubButtonLink, createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "Contrato DropSinglePhase",
		icon: "solidity",
	},
	title: "DropSinglePhase | Contrato de thirdweb",
	description:
		"El contrato inteligente DropSinglePhase es una extensión destinada para distribuir tokens ERC20 o ERC721.",
});

# DropSinglePhase (ERC20 & ERC721)

```solidity
import "@thirdweb-dev/contracts/extension/DropSinglePhase.sol";
```

El contrato inteligente `DropSinglePhase` es una extensión destinada para distribuir tokens ERC20 o ERC721.

Esta extensión implementa el mecanismo de distribución 'Drop': establece restricciones, conocidas como una fase de reclamo, tales como un precio a cobrar, una lista de permitidos, etc., para la acuñación pública / de lista permitida de tus tokens.

`DropSinglePhase` te permite establecer una fase de reclamo a diferencia de la extensión [`Drop`](/contracts/build/extensions/general/Drop).

<Callout title="info" variant="info">

Se recomienda usar esta extensión junto con la extensión [`LazyMint`](/contracts/build/extensions/general/LazyMint).

</Callout>

<br />

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/extension/DropSinglePhase.sol" />

## Uso

La extensión `DropSinglePhase` es un _contrato abstracto_, y espera que implementes las siguientes funciones por ti mismo:

| Nombre                     | Tipo                    | Descripción                                                                                                                                      |
| -------------------------- | ----------------------- | ------------------------------------------------------------------------------------------------------------------------------------------------ |
| `_afterClaim`              | internal _virtual_       | Se ejecuta después de cada llamada a la función `claim`.                                                                                         |
| `_beforeClaim`             | internal _virtual_       | Se ejecuta antes de cada llamada a la función `claim`.                                                                                          |
| `_collectPriceOnClaim`     | internal _virtual_       | Cobra y distribuye el valor de la venta primaria de los NFTs que están siendo reclamados.                                                        |
| `_transferTokensOnClaim`   | internal _virtual_       | Transfiere los NFTs que están siendo reclamados.                                                                                                |
| `_canSetClaimConditions`   | internal view _virtual_  | Se ejecuta en cada intento de establecer las condiciones de reclamo en el contrato. Devuelve si las condiciones de reclamo pueden establecerse en el contexto de ejecución dado. |

Este es un contrato inteligente de ejemplo que demuestra cómo heredar de esta extensión y sobrescribir las funciones para agregar (opcionalmente) funcionalidad personalizada.

```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/extension/DropSinglePhase.sol";
import "@thirdweb-dev/contracts/base/ERC20Base.sol";
import "@thirdweb-dev/contracts/lib/CurrencyTransferLib.sol";

/// Este es un EJEMPLO del uso de `DropSinglePhase` para distribuir tokens ERC20.

contract MyContract is ERC20Base, DropSinglePhase {
    /*//////////////////////////////////////////////////////////////
                            Constructor
    //////////////////////////////////////////////////////////////*/

    constructor(address _defaultAdmin, string memory _name, string memory _symbol)
        ERC20Base(_defaultAdmin, _name, _symbol)
    {}

    /*//////////////////////////////////////////////////////////////
                        Internal (overrideable) functions
    //////////////////////////////////////////////////////////////*/

    /// @dev Se ejecuta antes de cada llamada a la función `claim`.
    function _beforeClaim(
        uint256 _tokenId,
        address _receiver,
        uint256 _quantity,
        address _currency,
        uint256 _pricePerToken,
        AllowlistProof calldata _allowlistProof,
        bytes memory _data
    ) internal virtual override {
      // Your custom implementation logic here
    }

    /// @dev Se ejecuta después de cada llamada a la función `claim`.
    function _afterClaim(
        uint256 _tokenId,
        address _receiver,
        uint256 _quantity,
        address _currency,
        uint256 _pricePerToken,
        AllowlistProof calldata _allowlistProof,
        bytes memory _data
    ) internal virtual override {
      // Your custom implementation logic here
    }

   /// @dev Cobra y distribuye el valor de la venta primaria de los tokens que están siendo reclamados.
    function _collectPriceOnClaim(
        address _primarySaleRecipient,
        uint256 _quantityToClaim,
        address _currency,
        uint256 _pricePerToken
    ) internal virtual override {
        if (_pricePerToken == 0) {
            return;
        }

        uint256 totalPrice = (_quantityToClaim * _pricePerToken) / 1 ether;
        require(totalPrice > 0, "quantity too low");

        if (_currency == CurrencyTransferLib.NATIVE_TOKEN) {
            require(msg.value == totalPrice, "Must send total price.");
        }

        address saleRecipient = _primarySaleRecipient;
        CurrencyTransferLib.transferCurrency(
            _currency,
            msg.sender,
            saleRecipient,
            totalPrice
        );
    }

   /// @dev Transfiere los tokens que están siendo reclamados.
    function _transferTokensOnClaim(address _to, uint256 _quantityBeingClaimed)
        internal
        virtual
        override
        returns (uint256)
    {
        _mint(_to, _quantityBeingClaimed);
        return 0;
    }

  /// @dev Verifica si la información de la tarifa de la plataforma puede ser establecida en el contexto de ejecución dado.
    function _canSetClaimConditions()
        internal
        view
        virtual
        override
        returns (bool)
    {
        return msg.sender == owner();
    }
}
```

## Contratos Base que Implementan Esta Extensión

- [ERC721Drop](/contracts/build/base-contracts/erc-721/drop)
- [ERC20Drop](/contracts/build/extensions/erc-1155/ERC1155Claimable)

## Referencia Completa de la API

<Details summary="claim">

```solidity
function claim(
    address receiver,
    uint256 quantity,
    address currency,
    uint256 pricePerToken,
    AllowlistProof calldata allowlistProof,
    bytes memory data
) external payable;
```

- Permite que una cuenta reclame una cantidad dada de tokens.
- Parámetro `receiver`: El receptor de los tokens a reclamar.
- Parámetro `quantity`: La cantidad de tokens a reclamar.
- Parámetro `currency`: La moneda con la que pagar por el reclamo.
- Parámetro `pricePerToken`: El precio por token a pagar por el reclamo.
- Parámetro `allowlistProof`: La prueba de la inclusión del reclamante en la lista permitida del merkle root.
- Parámetro `data`: Datos arbitrarios en bytes que pueden ser utilizados en la implementación de esta interfaz.

</Details>

<Details summary="setClaimConditions">

```solidity
struct ClaimCondition {
    uint256 startTimestamp;
    uint256 maxClaimableSupply;
    uint256 supplyClaimed;
    uint256 quantityLimitPerWallet;
    bytes32 merkleRoot;
    uint256 pricePerToken;
    address currency;
    string metadata;
}

function setClaimConditions(ClaimCondition calldata phase, bool resetClaimEligibility) external;
```

- Permite que una billetera autorizada establezca las condiciones de reclamo para el contrato.
- Parámetro `phase`: El conjunto de restricciones que se aplicarán a la acuñación de tokens en el contrato.
  - `startTimestamp`: La marca de tiempo unix después de la cual se aplican las condiciones de reclamo.
  - `maxClaimableSupply`: El número total máximo de tokens que se pueden reclamar bajo la condición de reclamo.
  - `supplyClaimed`: En un momento dado, el número de tokens que han sido reclamados bajo la condición de reclamo.
  - `quantityLimitPerWallet`: El número máximo de tokens que puede reclamar una billetera.
  - `merkleRoot`: La lista de direcciones permitidas que pueden reclamar tokens bajo la condición de reclamo.
  - `pricePerToken`: El precio requerido para pagar por cada token reclamado.
  - `currency`: La moneda en la que debe pagarse el precio.
  - `metadata`: Metadatos de la condición de reclamo.
- Parámetro `resetClaimEligibility`: Si se deben trasladar las restricciones activas para las billeteras que reclamaron tokens en la condición de reclamo actual (por ejemplo, la siguiente marca de tiempo de reclamo válida para una billetera dada).
- La función `_canSetClaimConditions` se ejecuta en cada llamada a esta función. Si el valor devuelto de `_canSetClaimConditions` es `false`, la llamada a `setClaimConditions` se revertirá.

</Details>

<Details summary="verifyClaim">

```solidity
struct AllowlistProof {
    bytes32[] proof;
    uint256 quantityLimitPerWallet;
    uint256 pricePerToken;
    address currency;
}

function verifyClaim(
    address claimer,
    uint256 quantity,
    address currency,
    uint256 pricePerToken,
    AllowlistProof calldata allowlistProof
) public view returns (bool isOverride);
```

- Verifica una solicitud para reclamar tokens contra los criterios de la condición de reclamo activa.
- Parámetro `claimer`: La billetera que está reclamando los tokens.
- Parámetro `quantity`: La cantidad de tokens a reclamar.
- Parámetro `currency`: La moneda con la que pagar el precio por el reclamo.
- Parámetro `pricePerToken`: El precio a pagar por cada token reclamado.
- Parámetro `allowlistProof`: Estructura que contiene los siguientes elementos:
  - `proof`: Prueba de la inclusión de la billetera concerniente en una lista permitida.
  - `quantityLimitPerWallet`: La cantidad total de tokens que la billetera de la lista permitida es elegible para reclamar con el tiempo.
  - `pricePerToken`: El precio por token que la billetera de la lista permitida debe pagar para reclamar tokens.
  - `currency`: La moneda en la que la billetera de la lista permitida debe pagar el precio para reclamar tokens.

</Details>

<Details summary="getSupplyClaimedByWallet">

```solidity
function getSupplyClaimedByWallet(address claimer) public view returns (uint256);
```

- Devuelve el número de tokens reclamados por una billetera bajo la condición activa.
- Parámetro `claimer`: La billetera que está reclamando los tokens.

</Details>

<Details summary="_dropMsgSender">

```solidity
function _dropMsgSender() internal virtual returns (address);
```

- Expone la capacidad de sobrescribir el remitente del mensaje (msg sender).

</Details>

<Details summary="_beforeClaim">

```solidity
function _beforeClaim(
    address receiver,
    uint256 quantity,
    address currency,
    uint256 pricePerToken,
    AllowlistProof calldata allowlistProof,
    bytes memory data
) internal virtual
```

- Se ejecuta antes de cada llamada a la función `claim`. Expone la capacidad de agregar lógica personalizada que se ejecute antes de cada reclamo de tokens.
- Parámetro `receiver`: El receptor de los tokens a reclamar.
- Parámetro `quantity`: La cantidad de tokens a reclamar.
- Parámetro `currency`: La moneda con la que pagar por el reclamo.
- Parámetro `pricePerToken`: El precio por token a pagar por el reclamo.
- Parámetro `allowlistProof`: La prueba de la inclusión del reclamante en la lista permitida del merkle root.
- Parámetro `data`: Datos arbitrarios en bytes que pueden ser utilizados en la implementación de esta interfaz.

</Details>

<Details summary="_afterClaim">

```solidity
function _afterClaim(
    address receiver,
    uint256 quantity,
    address currency,
    uint256 pricePerToken,
    AllowlistProof calldata allowlistProof,
    bytes memory data
) internal virtual
```

- Se ejecuta después de cada llamada a la función `claim`. Expone la capacidad de agregar lógica personalizada que se ejecute después de cada reclamo de tokens.
- Parámetro `receiver`: El receptor de los tokens a reclamar.
- Parámetro `quantity`: La cantidad de tokens a reclamar.
- Parámetro `currency`: La moneda con la que pagar por el reclamo.
- Parámetro `pricePerToken`: El precio por token a pagar por el reclamo.
- Parámetro `allowlistProof`: La prueba de la inclusión del reclamante en la lista permitida del merkle root.
- Parámetro `data`: Datos arbitrarios en bytes que pueden ser utilizados en la implementación de esta interfaz.

</Details>

<Details summary="_collectPriceOnClaim">

```solidity
function _collectPriceOnClaim(
    address primarySaleRecipient,
    uint256 quantityToClaim,
    address currency,
    uint256 pricePerToken
) internal virtual;
```

- Cobra y distribuye el valor de la venta primaria de los tokens que están siendo reclamados.
- Parámetro `primarySaleRecipient`: El receptor del valor de la venta primaria generado a partir de un reclamo.
- Parámetro `quantityToClaim`: La cantidad de tokens que están siendo reclamados.
- Parámetro `currency`: La moneda utilizada para pagar el reclamo.
- Parámetro `pricePerToken`: El precio por token a pagar por el reclamo.

</Details>

<Details summary="_transferTokensOnClaim">

```solidity
function _transferTokensOnClaim(address to, uint256 quantityBeingClaimed)
    internal
    virtual
    returns (uint256 startTokenId);
```

- Transfiere los tokens que están siendo reclamados al receptor correspondiente.
- Parámetro `to`: El receptor de los tokens para el reclamo.
- Parámetro `quantityBeingClaimed`: La cantidad de tokens que están siendo reclamados.

</Details>

<Details summary="_canSetClaimConditions">

```solidity
function _canSetClaimConditions() internal view virtual returns (bool);
```

- Se ejecuta en cada llamada a la función `setClaimConditions`.
- Devuelve si las condiciones de reclamo pueden establecerse en el contexto de ejecución dado.
- Por ejemplo, esta función puede verificar si la billetera que llama a `setClaimConditions` es el propietario del contrato, y hacer cumplir que solo el propietario pueda llamar exitosamente a `setClaimConditions`.

</Details>
