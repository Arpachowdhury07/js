import { Details, Callout, GithubButtonLink, createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "Contrato DelayedReveal",
		icon: "solidity",
	},
	title: "DelayedReveal | Contrato de thirdweb",
	description:
		"El contrato inteligente DelayedReveal es una extensión para cualquier contrato NFT. Te permite crear lotes de NFTs con 'revelación retardada'.",
});


# DelayedReveal

```solidity
import "@thirdweb-dev/contracts/extension/DelayedReveal.sol";
```

El contrato inteligente [`DelayedReveal`](/glossary/delayed-reveal) es una extensión para cualquier contrato NFT. Te permite crear lotes de NFTs con 'revelación retardada'.

Para que las extensiones `ERC721Revealable` o `ERC1155Revealable` sean detectadas en el panel de control de tu contrato, es necesario que se herede el respectivo EIP
([`ERC721`](/contracts/build/extensions/erc-721/ERC721) o [`ERC1155`](/contracts/build/extensions/erc-1155/ERC1155)).

<Callout title="info" variant="info">
	Se recomienda usar esta extensión junto con la
	[`LazyMint`](/contracts/build/extensions/general/LazyMint) extensión.
</Callout>

<br />

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/extension/DelayedReveal.sol" />

## Uso

La extensión `DelayedReveal` es un _contrato abstracto_, y espera que implementes las siguientes funciones por ti mismo:

| Nombre                                                                | Tipo     | Retorna         | Descripción                             |
| --------------------------------------------------------------------- | -------- | --------------- | --------------------------------------- |
| [`reveal`](/contracts/build/extensions/general/DelayedReveal#reveal)  | external | `string memory` | Revela un lote de NFTs con revelación retardada. |

Este es un contrato inteligente de ejemplo que demuestra cómo heredar de esta extensión y sobrescribir las funciones para agregar (opcionalmente) funcionalidad personalizada.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/base/ERC721LazyMint.sol";
import "@thirdweb-dev/contracts/extension/DelayedReveal.sol";

/// Este es solo un contrato de EJEMPLO que utiliza `DelayedReveal`.

contract MyContract is ERC721LazyMint, DelayedReveal {
    using TWStrings for uint256;

    constructor(
        string memory _name,
        string memory _symbol,
        address _royaltyRecipient,
        uint128 _royaltyBps
    ) ERC721LazyMint(_name, _symbol, _royaltyRecipient, _royaltyBps) {}

    /**
     *  Sobrescribimos la función `lazyMint`, y usamos el parámetro `_data` para almacenar los metadatos cifrados
     *  para los NFTs de 'revelación retardada'.
     */
    function lazyMint(
        uint256 _amount,
        string calldata _baseURIForTokens,
        bytes calldata _data
    ) public override returns (uint256 batchId) {
        if (_data.length > 0) {
            (bytes memory encryptedURI, bytes32 provenanceHash) = abi.decode(
                _data,
                (bytes, bytes32)
            );
            if (encryptedURI.length != 0 && provenanceHash != "") {
                _setEncryptedData(nextTokenIdToLazyMint + _amount, _data);
            }
        }

        return super.lazyMint(_amount, _baseURIForTokens, _data);
    }

    /**
     *  Sobrescribimos `tokenURI` para devolver una URI adecuada para los NFTs cuyos metadatos reales están cifrados.
     */
    function tokenURI(uint256 _tokenId)
        public
        view
        virtual
        override
        returns (string memory)
    {
        (uint256 batchId, ) = _getBatchId(_tokenId);
        string memory batchUri = _getBaseURI(_tokenId);

        if (isEncryptedBatch(batchId)) {
            return string(abi.encodePacked(batchUri, "0"));
        } else {
            return string(abi.encodePacked(batchUri, _tokenId.toString()));
        }
    }

    /**
     *  Solo permitimos que el propietario del contrato revele los metadatos para un lote de NFTs.
     */
    function reveal(uint256 _index, bytes calldata _key)
        external
        override
        returns (string memory revealedURI)
    {
        require(msg.sender == owner(), "Not authorized");

        uint256 batchId = getBatchIdAtIndex(_index);
        revealedURI = getRevealURI(batchId, _key);

        _setEncryptedData(batchId, "");
        _setBaseURI(batchId, revealedURI);
    }
}
```

## Contratos Base que Implementan Esta Extensión

- [ERC721DelayedReveal](/contracts/build/base-contracts/erc-721/delayed-reveal)
- [ERC721Drop](/contracts/build/base-contracts/erc-721/drop)
- [ERC1155DelayedReveal](/contracts/build/base-contracts/erc-1155/delayed-reveal)
- [ERC1155Drop](/contracts/build/base-contracts/erc-1155/drop)

## Referencia Completa de la API

<Details summary="_setEncryptedData">

```solidity
function _setEncryptedData(uint256 batchId, bytes memory encryptedData) internal;
```

- Establece los datos de revelación retardada para un lote dado de NFTs.
- Parámetro `batchId`: El identificador para el lote de NFTs con revelación retardada.
- Parámetro `encryptedData`: Los metadatos cifrados para almacenar para el lote dado de NFTs.

</Details>

<Details summary="getRevealURI">

```solidity
function getRevealURI(uint256 batchId, bytes calldata key) public view returns (string memory revealedURI);
```

- Devuelve la URI revelada, es decir, la URI de metadatos reales para un lote de NFTs.
- Parámetro `batchId`: El identificador para el lote de NFTs con revelación retardada cuya URI de metadatos reales se está recuperando.
- Parámetro `key`: Clave segura utilizada por el llamador/admin para cifrar inicialmente los metadatos del lote relevante de NFTs.

</Details>

<Details summary="encryptDecrypt">

```solidity
function encryptDecrypt(bytes memory data, bytes calldata key) public pure override returns (bytes memory result);
```

- Implementación estándar de cifrado XOR en Solidity. Se usa para cifrar los metadatos de los NFTs.
- Aprende más sobre el uso de esta función en este [blogpost](https://blog.thirdweb.com/delayed-reveal-nfts).

</Details>

<Details summary="isEncryptedBatch">

```solidity
function isEncryptedBatch(uint256 batchId) public view returns (bool);
```

- Devuelve si el lote relevante de NFTs está sujeto a una revelación retardada.
- Parámetro `batchId`: El identificador para un lote de NFTs.

</Details>

<Details summary="reveal">

```solidity
function reveal(uint256 batchId, bytes calldata key) external returns (string memory revealedURI);
```

- Revela un lote de NFTs con revelación retardada.
- Parámetro `batchId`: El ID para el lote de NFTs con revelación retardada a revelar.
- Parámetro `key`: La clave con la cual se cifró la URI base para el lote relevante de NFTs.

</Details>
