import { Details, Callout, GithubButtonLink, createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "Contrato Drop",
		icon: "solidity",
	},
	title: "Drop | Contrato de thirdweb",
	description:
		"Drop es una extensión destinada para distribuir tokens ERC20 o ERC721. Permite configurar múltiples fases de reclamo a diferencia de DropSinglePhase",
});

# Drop (ERC20 & ERC721)

```solidity
import "@thirdweb-dev/contracts/extension/Drop.sol";
```

[`Drop`](/glossary/drop) es una extensión destinada para distribuir tokens ERC20 o ERC721. Permite configurar múltiples fases de reclamo a diferencia de `DropSinglePhase`. Esta serie de fases de reclamo se ordena por su respectivo `startTimestamp`.

Esta extensión implementa el ['mecanismo de distribución Drop'](/glossary/drop): establece múltiples fases de reclamo con restricciones como un precio a cobrar, una lista de permitidos, etc., para la acuñación pública / de lista permitida de tus tokens.

<Callout title="info" variant="info">

Se recomienda usar esta extensión junto con la extensión [`LazyMint`](/contracts/build/extensions/general/LazyMint).

</Callout>


<br />

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/extension/Drop.sol" />

## Uso

La extensión `Drop` es un _contrato abstracto_, y espera que implementes las siguientes funciones por ti mismo:

| Nombre                    | Tipo                     | Descripción                                                                                                                                      |
| ------------------------- | ------------------------ | ------------------------------------------------------------------------------------------------------------------------------------------------ |
| `_afterClaim`              | internal _virtual_        | Se ejecuta después de cada llamada a la función `claim`.                                                                                         |
| `_beforeClaim`             | internal _virtual_        | Se ejecuta antes de cada llamada a la función `claim`.                                                                                          |
| `_collectPriceOnClaim`     | internal _virtual_        | Cobra y distribuye el valor de la venta primaria de los NFTs que están siendo reclamados.                                                        |
| `_transferTokensOnClaim`   | internal _virtual_        | Transfiere los NFTs que están siendo reclamados.                                                                                               |
| `_canSetClaimConditions`   | internal view _virtual_   | Se ejecuta en cada intento de establecer las condiciones de reclamo en el contrato. Devuelve si las condiciones de reclamo pueden establecerse en el contexto de ejecución dado. |

Este es un contrato inteligente de ejemplo que demuestra cómo heredar de esta extensión y sobrescribir las funciones para agregar (opcionalmente) funcionalidad personalizada.

```solidity
// SPDX-License-Identifier: Apache-2.0
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/extension/Drop.sol";
import "@thirdweb-dev/contracts/base/ERC20Base.sol";
import "@thirdweb-dev/contracts/lib/CurrencyTransferLib.sol";

/// Este es un EJEMPLO de uso de `Drop` para distribuir tokens ERC20.

contract MyContract is ERC20Base, Drop {
    /*//////////////////////////////////////////////////////////////
                            Constructor
    //////////////////////////////////////////////////////////////*/

    constructor(address _defaultAdmin, string memory _name, string memory _symbol)
        ERC20Base(_defaultAdmin, _name, _symbol)
    {}

    /*//////////////////////////////////////////////////////////////
                        Internal (overrideable) functions
    //////////////////////////////////////////////////////////////*/

    /// @dev Runs before every `claim` function call.
    function _beforeClaim(
        uint256 _tokenId,
        address _receiver,
        uint256 _quantity,
        address _currency,
        uint256 _pricePerToken,
        AllowlistProof calldata _allowlistProof,
        bytes memory _data
    ) internal virtual override {
      // Your custom implementation logic here
    }

    /// @dev Runs after every `claim` function call.
    function _afterClaim(
        uint256 _tokenId,
        address _receiver,
        uint256 _quantity,
        address _currency,
        uint256 _pricePerToken,
        AllowlistProof calldata _allowlistProof,
        bytes memory _data
    ) internal virtual override {
      // Your custom implementation logic here
    }

    /// @dev Collects and distributes the primary sale value of tokens being claimed.
    function _collectPriceOnClaim(
        address _primarySaleRecipient,
        uint256 _quantityToClaim,
        address _currency,
        uint256 _pricePerToken
    ) internal virtual override {
        if (_pricePerToken == 0) {
            return;
        }

        uint256 totalPrice = (_quantityToClaim * _pricePerToken) / 1 ether;
        require(totalPrice > 0, "quantity too low");

        if (_currency == CurrencyTransferLib.NATIVE_TOKEN) {
            require(msg.value == totalPrice, "Must send total price.");
        }

        address saleRecipient = _primarySaleRecipient;
        CurrencyTransferLib.transferCurrency(
            _currency,
            msg.sender,
            saleRecipient,
            totalPrice
        );
    }

    /// @dev Transfers the tokens being claimed.
    function _transferTokensOnClaim(address _to, uint256 _quantityBeingClaimed)
        internal
        virtual
        override
        returns (uint256)
    {
        _mint(_to, _quantityBeingClaimed);
        return 0;
    }

    /// @dev Checks whether platform fee info can be set in the given execution context.
    function _canSetClaimConditions()
        internal
        view
        virtual
        override
        returns (bool)
    {
        return msg.sender == owner();
    }
}
```

## Contratos Base que Implementan Esta Extensión

Ninguno de los contratos base implementa esta extensión.

## Referencia Completa de la API

<Details summary="claim">

```solidity
function claim(
    address receiver,
    uint256 quantity,
    address currency,
    uint256 pricePerToken,
    AllowlistProof calldata allowlistProof,
    bytes memory data
) external payable;
```

- Permite que una cuenta reclame una cantidad dada de tokens.
- Parámetro `receiver`: El receptor de los tokens a reclamar.
- Parámetro `quantity`: La cantidad de tokens a reclamar.
- Parámetro `currency`: La moneda con la que pagar por el reclamo.
- Parámetro `pricePerToken`: El precio por token a pagar por el reclamo.
- Parámetro `allowlistProof`: La prueba de la inclusión del reclamante en la lista de permitidos del merkle root.
- Parámetro `data`: Datos arbitrarios en bytes que pueden ser utilizados en la implementación de esta interfaz.

</Details>

<Details summary="setClaimConditions">

```solidity
struct ClaimCondition {
    uint256 startTimestamp;
    uint256 maxClaimableSupply;
    uint256 supplyClaimed;
    uint256 quantityLimitPerWallet;
    bytes32 merkleRoot;
    uint256 pricePerToken;
    address currency;
    string metadata;
}

function setClaimConditions(ClaimCondition[] calldata phases, bool resetClaimEligibility) external;
```

- Permite que una billetera autorizada establezca las condiciones de reclamo para el contrato.
- Parámetro `phases`: El arreglo donde cada `phase` contiene un conjunto de restricciones que se aplicarán a la acuñación de tokens en el contrato.
  - `startTimestamp`: La marca de tiempo unix después de la cual se aplican las condiciones de reclamo. La misma condición de reclamo se aplica hasta el `startTimestamp` de la siguiente condición de reclamo.
  - `maxClaimableSupply`: El número total máximo de tokens que se pueden reclamar bajo la condición de reclamo.
  - `supplyClaimed`: En un momento dado, el número de tokens que han sido reclamados bajo la condición de reclamo.
  - `quantityLimitPerWallet`: El número máximo de tokens que puede reclamar una billetera.
  - `merkleRoot`: La lista de direcciones permitidas que pueden reclamar tokens bajo la condición de reclamo.
  - `pricePerToken`: El precio que debe pagarse por cada token reclamado.
  - `currency`: La moneda en la que debe pagarse el precio.
  - `metadata`: Metadatos de la condición de reclamo.
- Parámetro `resetClaimEligibility`: Si se deben trasladar las restricciones activas para las billeteras que reclamaron tokens en la condición de reclamo actual (por ejemplo, la siguiente marca de tiempo de reclamo válida para una billetera dada).
- La función `_canSetClaimConditions` se ejecuta en cada llamada a esta función. Si el valor devuelto de `_canSetClaimConditions` es `false`, la llamada a `setClaimConditions` se revertirá.

</Details>

<Details summary="verifyClaim">

```solidity
struct AllowlistProof {
    bytes32[] proof;
    uint256 quantityLimitPerWallet;
    uint256 pricePerToken;
    address currency;
}

function verifyClaim(
    uint256 conditionId,
    address claimer,
    uint256 quantity,
    address currency,
    uint256 pricePerToken,
    AllowlistProof calldata allowlistProof
) public view returns (bool isOverride);
```

- Verifica una solicitud para reclamar tokens contra los criterios de la condición de reclamo activa.
- Parámetro `conditionId`: ID de la condición de reclamo activa.
- Parámetro `claimer`: La billetera que está reclamando los tokens.
- Parámetro `quantity`: La cantidad de tokens a reclamar.
- Parámetro `currency`: La moneda con la que pagar el precio por el reclamo.
- Parámetro `pricePerToken`: El precio a pagar por cada token reclamado.
- Parámetro `allowlistProof`: Estructura que contiene los siguientes elementos:
  - `proof`: Prueba de la inclusión de la billetera en una lista permitida.
  - `quantityLimitPerWallet`: La cantidad total de tokens que la billetera de la lista permitida es elegible para reclamar con el tiempo.
  - `pricePerToken`: El precio por token que la billetera de la lista permitida debe pagar para reclamar tokens.
  - `currency`: La moneda en la que la billetera de la lista permitida debe pagar el precio para reclamar tokens.

</Details>

<Details summary="getActiveClaimConditionId">

```solidity
function getActiveClaimConditionId() public view returns (uint256);
```

- Devuelve el ID de la condición de reclamo actualmente activa.

</Details>

<Details summary="getClaimConditionById">

```solidity
function getClaimConditionById(uint256 conditionId) external view returns (ClaimCondition memory);
```

- Devuelve la condición de reclamo para un ID de condición dado.
- Parámetro `conditionId`: ID de la condición de reclamo que se está visualizando.

</Details>

<Details summary="getSupplyClaimedByWallet">

```solidity
function getSupplyClaimedByWallet(uint256 conditionId, address claimer) public view returns (uint256);
```

- Returns the number of tokens claimed by a wallet under the given condition.
- Parameter `conditionId`: Id of the claim condition under which to check the supply claimed.
- Parameter `claimer`: The wallet claiming tokens.

</Details>

<Details summary="_dropMsgSender">

```solidity
function _dropMsgSender() internal virtual returns (address);
```

- Exposes the ability to override the msg sender.

</Details>

<Details summary="_beforeClaim">

```solidity
function _beforeClaim(
    address receiver,
    uint256 quantity,
    address currency,
    uint256 pricePerToken,
    AllowlistProof calldata allowlistProof,
    bytes memory data
) internal virtual
```

- Runs before every `claim` function call. Exposes the ability to add custom logic that runs before every claim of tokens.
- Parameter `receiver`: The receiver of the tokens to claim.
- Parameter `quantity`: The quantity of tokens to claim.
- Parameter `currency`: The currency in which to pay for the claim.
- Parameter `pricePerToken`: The price per token to pay for the claim.
- Parameter `allowlistProof`: The proof of the claimer's inclusion in the merkle root allowlist.
- Parameter `data`: Arbitrary bytes data that can be leveraged in the implementation of this interface.

</Details>

<Details summary="_afterClaim">

```solidity
function _afterClaim(
    address receiver,
    uint256 quantity,
    address currency,
    uint256 pricePerToken,
    AllowlistProof calldata allowlistProof,
    bytes memory data
) internal virtual
```

- Runs after every `claim` function call. Exposes the ability to add custom logic that runs after every claim of tokens.
- Parameter `receiver`: The receiver of the tokens to claim.
- Parameter `quantity`: The quantity of tokens to claim.
- Parameter `currency`: The currency in which to pay for the claim.
- Parameter `pricePerToken`: The price per token to pay for the claim.
- Parameter `allowlistProof`: The proof of the claimer's inclusion in the merkle root allowlist.
- Parameter `data`: Arbitrary bytes data that can be leveraged in the implementation of this interface.

</Details>

<Details summary="_collectPriceOnClaim">

```solidity
function _collectPriceOnClaim(
    address primarySaleRecipient,
    uint256 quantityToClaim,
    address currency,
    uint256 pricePerToken
) internal virtual;
```

- Collects and distributes the primary sale value of tokens being claimed.
- Parameter `primarySaleRecipient`: The recipient of the primary sale value generated from a claim.
- Parameter `quantityToClaim`: The quantity of tokens being claimed.
- Parameter `currency`: The currency used to pay for the claim.
- Parameter `pricePerToken`: The price per token to pay for the claim.

</Details>

<Details summary="_transferTokensOnClaim">

```solidity
function _transferTokensOnClaim(address to, uint256 quantityBeingClaimed)
    internal
    virtual
    returns (uint256 startTokenId);
```

- Transfers the tokens being claimed to the appropriate recipient.
- Parameter `to`: The recipient of the tokens for a claim.
- Parameter `quantityBeingClaimed`: The quantity of tokens being claimed.

</Details>

<Details summary="_canSetClaimConditions">

```solidity
function _canSetClaimConditions() internal view virtual returns (bool);
```

- Runs on every `setClaimConditions` function call.
- Returns whether the claim condition can be set in the given execution context.
- For example, this function can check whether the wallet calling `setClaimConditions` is the contract owner, and enforce that only the owner should be able to successfully call `setClaimConditions`.

</Details>
