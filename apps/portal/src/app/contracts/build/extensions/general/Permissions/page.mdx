import { Details, GithubButtonLink, createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "Contrato de Permisos",
		icon: "solidity",
	},
	title: "Permisos | Contrato de thirdweb",
	description:
		"La extensión de Permisos proporciona control de acceso basado en roles: puedes crear roles y escribir lógica personalizada en tu contrato inteligente que dependa de si una billetera dada tiene un rol específico",
});

# Permisos

```solidity
import "@thirdweb-dev/contracts/extension/Permissions.sol";
```

La extensión `Permissions` proporciona control de acceso basado en roles: puedes crear roles y escribir lógica personalizada en tu contrato inteligente que dependa de si una billetera dada tiene un rol específico.

Existen dos contratos diferentes que puedes implementar para agregar permisos:

- `Permissions`: Crea roles y restringe el acceso a funciones según esos roles.
- [`PermissionsEnumerable`](/contracts/build/extensions/general/PermissionsEnumerable): `Permissions` más la capacidad de ver todas las direcciones que tienen un rol específico (o todos los roles).

Solo necesitas implementar **uno** de los dos.

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/extension/Permissions.sol" />

## Uso

Este es un contrato inteligente de ejemplo que demuestra cómo heredar de esta extensión y sobrescribir las funciones para agregar (opcionalmente) funcionalidad personalizada.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/extension/Permissions.sol";

contract MyContract is Permissions {
    // Any `bytes32` value is a valid role. You can create roles by defining them like this.
    bytes32 public constant NUMBER_ROLE = keccak256("NUMBER_ROLE");

    // See comments for `setNumber`, below.
    uint256 public number;

    /**
     *  El contrato `Permissions` hace disponible un rol ya definido: el `DEFAULT_ADMIN_ROLE`.
     *
     *  Como EJEMPLO, otorgamos a quien despliegue el contrato este rol de administrador.
     */
    constructor() {
        _setupRole(DEFAULT_ADMIN_ROLE, msg.sender);
    }

    /**
     *  EJEMPLO: aquí tenemos una función que queremos restringir solo a los poseedores de `NUMBER_ROLE`.
     *
     *  Para lograr esto, usamos el modificador `onlyRole` proporcionado por `Permissions`, y
     *  le pasamos `NUMBER_ROLE` como argumento.
     */
    function setNumber(uint256 _newNumber) public onlyRole(NUMBER_ROLE) {
        number = _newNumber;
    }
}
```

## Contratos Base que Implementan Esta Extensión

Ninguno de los contratos base implementa esta extensión.

## Referencia Completa de la API

<Details summary="hasRole">

```solidity
function hasRole(bytes32 role, address account) external view returns (bool);
```

- Devuelve `true` si a `account` se le ha otorgado el rol `role`.

</Details>

<Details summary="getRoleAdmin">

```solidity
function getRoleAdmin(bytes32 role) external view returns (bytes32);
```

- Devuelve el rol de administrador que controla `role`, es decir, los poseedores del rol de administrador (el valor devuelto) pueden otorgar o revocar `role` a las billeteras.

</Details>

<Details summary="grantRole">

```solidity
function grantRole(bytes32 role, address account) external;
```

- Otorga `role` a `account`. Solo puede ser llamada por un poseedor del rol de administrador del rol que se está otorgando.

</Details>

<Details summary="revokeRole">

```solidity
function revokeRole(bytes32 role, address account) external;
```

- Revoca `role` de `account`. Solo puede ser llamada por un poseedor del rol de administrador del rol que se está revocando.

</Details>

<Details summary="renounceRole">

```solidity
function renounceRole(bytes32 role, address account) external;
```

- Revoca `role` de la cuenta que realiza la llamada.

</Details>

<Details summary="hasRoleWithSwitch">

```solidity
function hasRoleWithSwitch(bytes32 role, address account) public view returns (bool);
```

- Devuelve `true` si a `account` se le ha otorgado el rol `role`.
- Las restricciones de rol pueden ser activadas o desactivadas:
  - Si `address(0)` tiene el rol `role`, entonces las restricciones de rol no se aplican, es decir, esta función siempre devolverá `true`.
  - Si `address(0)` no tiene el rol `role`, entonces las restricciones de rol se aplicarán, es decir, esta función se comportará como `hasRole`.

</Details>
