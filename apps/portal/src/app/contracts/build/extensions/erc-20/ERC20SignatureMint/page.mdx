import { Details, GithubButtonLink } from "@doc";
import { createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "Contrato ERC20SignatureMint",
		icon: "solidity",
	},
	title: "ERC20SignatureMint | contrato thirdweb",
	description:
		"El contrato SignatureMintERC20 es una extensión destinada a distribuir tokens ERC20",
});


# ERC20SignatureMint

```solidity
import "@thirdweb-dev/contracts/extension/SignatureMintERC20.sol";
```

El contrato `SignatureMintERC20` es una extensión destinada a distribuir tokens ERC20.

El mecanismo de ['firma de acuñación'](/glossary/signature-based-minting) en la extensión `SignatureMintERC20` utiliza [EIP-712](https://eips.ethereum.org/EIPS/eip-712), y es una forma en que un administrador de contrato puede autorizar la solicitud de una parte externa para acuñar tokens en el contrato del administrador. A grandes rasgos, esto significa que puedes autorizar a una parte externa a acuñar tokens en tu contrato y especificar qué exactamente será acuñado por esa parte externa.

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/extension/SignatureMintERC20.sol" />

## Uso

La extensión `SignatureMintERC20` es un contrato _abstracto_, y espera que implementes las siguientes funciones por ti mismo:

| Nombre                                        | Tipo                    | Descripción                                                                                                                                     |
| --------------------------------------------- | ----------------------- | ----------------------------------------------------------------------------------------------------------------------------------------------- |
| [`_canSignMintRequest`](#full-api-reference)  | internal view _virtual_ | Se ejecuta en cada intento de acuñar tokens con firma en el contrato. Devuelve si el firmante está autorizado para emitir una firma para la acuñación. |
| [`mintWithSignature`](#full-api-reference)    | external payable        | Acuña tokens de acuerdo con la solicitud de acuñación proporcionada.                                                                               |

Este es un ejemplo de contrato inteligente que demuestra cómo heredar de esta extensión y sobrescribir las funciones para agregar (opcionalmente) funcionalidades personalizadas.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/base/ERC20Base.sol";
import "@thirdweb-dev/contracts/extension/SignatureMintERC20.sol";
import "@thirdweb-dev/contracts/lib/CurrencyTransferLib.sol";

contract ERC20SignatureMint is ERC20Base, SignatureMintERC20 {
    /*//////////////////////////////////////////////////////////////
                            Constructor
    //////////////////////////////////////////////////////////////*/

    constructor(address _defaultAdmin, string memory _name, string memory _symbol)
        ERC20Base(_defaultAdmin, _name, _symbol)
    {}

    /// @dev Mints tokens according to the provided mint request.
    function mintWithSignature(
        MintRequest calldata _req,
        bytes calldata _signature
    ) external payable virtual returns (address signer) {
        require(_req.quantity > 0, "Minting zero tokens.");

        // Verify and process payload.
        signer = _processRequest(_req, _signature);

        address receiver = _req.to;

        // Collect price
        _collectPriceOnClaim(
            _req.primarySaleRecipient,
            _req.quantity,
            _req.currency,
            _req.pricePerToken
        );

        // Mint tokens.
        _mint(receiver, _req.quantity);

        emit TokensMintedWithSignature(signer, receiver, _req);
    }

    /// @dev Returns whether a given address is authorized to sign mint requests.
    function _canSignMintRequest(address _signer)
        internal
        view
        virtual
        override
        returns (bool)
    {
        return _signer == owner();
    }

    /// @dev Collects and distributes the primary sale value of tokens being claimed.
    function _collectPriceOnClaim(
        address _primarySaleRecipient,
        uint256 _quantityToClaim,
        address _currency,
        uint256 _pricePerToken
    ) internal virtual {
        if (_pricePerToken == 0) {
            return;
        }

        uint256 totalPrice = (_quantityToClaim * _pricePerToken) / 1 ether;
        require(totalPrice > 0, "quantity too low");

        if (_currency == CurrencyTransferLib.NATIVE_TOKEN) {
            require(msg.value == totalPrice, "Must send total price.");
        }

        address saleRecipient = _primarySaleRecipient;
        CurrencyTransferLib.transferCurrency(
            _currency,
            msg.sender,
            saleRecipient,
            totalPrice
        );
    }
}
```

## Contratos de Base que implementan esta extensión

- [ERC20SignatureMint](/contracts/build/base-contracts/erc-20/signature-mint)
- [ERC20SignatureMintVote](/contracts/build/base-contracts/erc-20/signature-mint-vote)

## Referencia completa de la API

<Details summary="verify">

```solidity
struct MintRequest {
    address to;
    address primarySaleRecipient;
    uint256 quantity;
    uint256 pricePerToken;
    address currency;
    uint128 validityStartTimestamp;
    uint128 validityEndTimestamp;
    bytes32 uid;
}

function verify(MintRequest calldata req, bytes calldata signature)
    public
    view
    returns (bool success, address signer)
```

- Verifica que una solicitud de acuñación sea válida y esté firmada por una billetera autorizada.
- Parámetro `req`: La solicitud de acuñación.
  - `to`: El destinatario de los tokens a acuñar.
  - `primarySaleRecipient`: El destinatario de los ingresos por ventas primarias del token acuñado.
  - `quantity`: La cantidad de tokens a acuñar.
  - `pricePerToken`: El precio a pagar por la cantidad de tokens acuñados.
  - `currency`: La moneda en la que se debe pagar el precio por token acuñado.
  - `validityStartTimestamp`: El sello de tiempo unix después del cual la carga útil es válida.
  - `validityEndTimestamp`: El sello de tiempo unix en el cual la carga útil expira.
  - `uid`: Un identificador único para la carga útil.
- Parámetro `signature`: La firma producida por una billetera autorizada que firma la solicitud de acuñación.

</Details>

<Details summary="mintWithSignature">

```solidity
struct MintRequest {
    address to;
    address primarySaleRecipient;
    uint256 quantity;
    uint256 pricePerToken;
    address currency;
    uint128 validityStartTimestamp;
    uint128 validityEndTimestamp;
    bytes32 uid;
}

function mintWithSignature(MintRequest calldata req, bytes calldata signature)
    external
    payable
    returns (address signer)
```

- Acuña tokens según la solicitud de acuñación proporcionada.
- Parámetro `req`: La solicitud de acuñación.
  - `to`: El destinatario de los tokens a acuñar.
  - `primarySaleRecipient`: El destinatario de los ingresos por ventas primarias del token acuñado.
  - `quantity`: La cantidad de tokens a acuñar.
  - `pricePerToken`: El precio a pagar por la cantidad de tokens acuñados.
  - `currency`: La moneda en la que se debe pagar el precio por token acuñado.
  - `validityStartTimestamp`: El sello de tiempo unix después del cual la carga útil es válida.
  - `validityEndTimestamp`: El sello de tiempo unix en el cual la carga útil expira.
  - `uid`: Un identificador único para la carga útil.
- Parámetro `signature`: La firma producida por una billetera autorizada que firma la solicitud de acuñación.

</Details>

<Details summary="_processRequest">

```solidity
struct MintRequest {
    address to;
    address primarySaleRecipient;
    uint256 quantity;
    uint256 pricePerToken;
    address currency;
    uint128 validityStartTimestamp;
    uint128 validityEndTimestamp;
    bytes32 uid;
}

function _processRequest(MintRequest calldata req, bytes calldata signature)
    internal
    returns (address signer)
```

- Verifica una solicitud de acuñación y marca la solicitud y la firma como utilizadas.
- Parámetro `req`: La solicitud de acuñación.
  - `to`: El destinatario de los tokens a acuñar.
  - `primarySaleRecipient`: El destinatario de los ingresos por ventas primarias del token acuñado.
  - `quantity`: La cantidad de tokens a acuñar.
  - `pricePerToken`: El precio a pagar por la cantidad de tokens acuñados.
  - `currency`: La moneda en la que se debe pagar el precio por token acuñado.
  - `validityStartTimestamp`: El sello de tiempo unix después del cual la carga útil es válida.
  - `validityEndTimestamp`: El sello de tiempo unix en el cual la carga útil expira.
  - `uid`: Un identificador único para la carga útil.
- Parámetro `signature`: La firma producida por una billetera autorizada que firma la solicitud de acuñación.

</Details>

<Details summary="_recoverAddress">

```solidity
struct MintRequest {
    address to;
    address primarySaleRecipient;
    uint256 quantity;
    uint256 pricePerToken;
    address currency;
    uint128 validityStartTimestamp;
    uint128 validityEndTimestamp;
    bytes32 uid;
}

function _recoverAddress(MintRequest calldata req, bytes calldata signature)
    internal
    view
    returns (address signer)
```

- Devuelve la dirección del firmante de la solicitud de acuñación.
- Parámetro `req`: La solicitud de acuñación.
  - `to`: El destinatario de los tokens a acuñar.
  - `primarySaleRecipient`: El destinatario de los ingresos por ventas primarias del token acuñado.
  - `quantity`: La cantidad de tokens a acuñar.
  - `pricePerToken`: El precio a pagar por la cantidad de tokens acuñados.
  - `currency`: La moneda en la que se debe pagar el precio por token acuñado.
  - `validityStartTimestamp`: El sello de tiempo unix después del cual la carga útil es válida.
  - `validityEndTimestamp`: El sello de tiempo unix en el cual la carga útil expira.
  - `uid`: Un identificador único para la carga útil.
- Parámetro `signature`: La firma producida por una billetera autorizada que firma la solicitud de acuñación.

</Details>

<Details summary="_canSignMintRequest">

```solidity
function _canSignMintRequest(address signer) internal view virtual returns (bool);
```

- Devuelve si una dirección dada está autorizada para firmar solicitudes de acuñación.
- Parámetro `signer`: la dirección que el contrato verifica si es elegible para firmar solicitudes de acuñación o no.

</Details>
