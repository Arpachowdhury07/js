import { Details, GithubButtonLink } from "@doc";
import { createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "Contrato ERC20Staking",
		icon: "solidity",
	},
	title: "ERC20Staking | contrato de thirdweb",
	description:
		"La extensión Staking20 de smart contract implementa el mecanismo de staking ERC20. Con esta extensión, puedes configurar un contrato de staking para los poseedores de tus tokens ERC20",
});

# ERC20Staking

```solidity
import "@thirdweb-dev/contracts/extension/Staking20.sol";
```

La extensión `Staking20` de smart contract implementa el mecanismo de staking ERC20. Con esta extensión, puedes configurar un contrato de staking para los poseedores de tus tokens ERC20. Los usuarios pueden hacer staking de sus tokens y ganar un conjunto diferente de tokens ERC20 como recompensas.

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/extension/Staking20.sol" />

## Uso

Este es un ejemplo de smart contract que demuestra cómo heredar de esta extensión y sobrescribir las funciones para agregar funcionalidad personalizada (opcional).

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/extension/Staking20.sol";
import "@thirdweb-dev/contracts/eip/interface/IERC20.sol";
import "@thirdweb-dev/contracts/eip/interface/IERC20Metadata.sol";

contract MyContract is Staking20 {
    // Dirección del token de recompensa ERC20. Ver {_mintRewards}.
    address public rewardToken;

    /**
    *  Solo almacenamos la dirección del desplegador del contrato para los fines del ejemplo
    *  en el comentario del código abajo.
    *
    *  Hacer esto no es necesario para usar la extensión `Staking20`.
    */

    address public deployer;

    constructor(
        uint256 _timeUnit,
        uint256 _rewardRatioNumerator,
        uint256 _rewardRatioDenominator,
        address _stakingToken,
        address _rewardToken,
        address _nativeTokenWrapper
    ) Staking20(
            _nativeTokenWrapper,
            _stakingToken,
            IERC20Metadata(_stakingToken).decimals(),
            IERC20Metadata(_rewardToken).decimals()
    ) {
        _setStakingCondition(_timeUnit, _rewardRatioNumerator, _rewardRatioDenominator);

        rewardToken = _rewardToken;
        deployer = msg.sender;
    }

    /**
    *  @dev    Acumular/Transferir recompensas ERC20 al staker. Debe sobrescribirse.
    *
    *  @param _staker    Dirección a la que se enviarán las recompensas.
    *  @param _rewards   Cantidad de tokens a ser entregados como recompensa.
    *
    */

    function _mintRewards(address _staker, uint256 _rewards) internal override {
        IERC20(rewardToken).transfer(_staker, _rewards);
    }

    // Returns whether staking restrictions can be set in given execution context.
    function _canSetStakeConditions() internal view override returns (bool) {
        return msg.sender == deployer;
    }
}
```

## Contratos Base que Implementan Esta Extensión

- [Staking20Base](/contracts/build/base-contracts/erc-20/staking)

## Referencia Completa de la API

<Details summary="stake">

```solidity
function stake(uint256 amount) external;
```

- Permite a un usuario hacer staking de una cantidad de tokens ERC20 pasando la cantidad.
- Parámetro `amount`: Cantidad de tokens a poner en staking.

</Details>

<Details summary="withdraw">

```solidity
function withdraw(uint256 amount) external payable;
```

- Retira los tokens del staking y los retira del contrato.
- Parámetro `amount`: Cantidad de tokens a retirar.

</Details>

<Details summary="claimRewards"s>

```solidity
function claimRewards() external;
```

- Reclama las recompensas acumuladas. Este método de reclamo permite un mecanismo de extracción donde los usuarios deben iniciar el reclamo de las recompensas.

</Details>

<Details summary="getStakeInfo">

```solidity
function getStakeInfo(address staker) external view returns (uint256 tokensStaked, uint256 rewards);
```

- Ver el número de tokens apostados y las recompensas totales disponibles para un usuario.
- Parámetro `staker`: Dirección de la cuenta del apostador.

</Details>

<Details summary="setRewardRation">

```solidity
function setRewardRatio(uint256 numerator, uint256 denominator) external
```

- Permite a una cuenta autorizada establecer la proporción de recompensas. Interpretada como (numerador/denominador) recompensas por segundo/día/etc., según la unidad de tiempo. Por ejemplo, una proporción de 1/20 significaría 1 token de recompensa por cada 20 tokens apostados.
- Parámetro `numerator`: Numerador de la proporción de recompensas.
- Parámetro `denominator`: Denominador de la proporción de recompensas.

</Details>

<Details summary="setTimeUnit">

```solidity
function setTimeUnit(uint256 timeUnit) external;
```

- Permite a una cuenta autorizada establecer la unidad de tiempo como un número de segundos. Por ejemplo, 1 hora puede establecerse como 3600 segundos, lo que establece la frecuencia de recompensas como _por hora_.
- Parámetro `timeUnit`: Nueva unidad de tiempo.

</Details>
